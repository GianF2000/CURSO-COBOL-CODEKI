       IDENTIFICATION DIVISION.
       PROGRAM-ID PGMVAC13.
      **********************************************************
      *                                                        *
      *                     ASINCRONICO 08                     *
      *                                                        *
      **********************************************************
      *  Validar todos los campos del arch entrada; si va ok   *
      *  entonces mover valores de arch entrada a salida y     *
      *  contar registros que cumplen con los filtros          *
      *****************************************************************
      *   JCL    *                  ARCHIVOS                          *
      *****************************************************************
      * COMPILA  * E1CLIEN: KC03C12.FIX.SINCR14.PGMVAC12.E1.NOVCLIEN  *
      * JCLEJEM1 * S1CLIOK: KC03C12.FIX.ASINCR08.PGMVAC13.S1.NOVCLIEN *
      *                                                               *
      *****************************************************************
      ****************************************
      *      MANTENIMIENTO DE PROGRAMA       *
      ****************************************
      *  FECHA     *    DETALLE        * COD *
      ****************************************
      * 15-04-2025 * Creacion          * 001 *
      *            *                   *     *
      ****************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
      * E1CLIEN es la variable interna del archivo de entrada y se
      * asigna al DDCLIEN que es variable externa establecida en el JOB

      * S1CLIOK es la variable interna del archivo de salida y se
      * asigna al DDCLIOK que es variable externa establecida en el JOB

      * El FS es una validador interno del estado del archivo asignado
       FILE-CONTROL.
           SELECT         E1CLIEN
           ASSIGN         DDCLIEN
           FILE STATUS IS FS-E1CLIEN
           .

           SELECT         S1CLIOK
           ASSIGN         DDCLIOK
           FILE STATUS IS FS-S1CLIOK
           .

      * Establecemos que E1CLIEN es de 50 caracteres alfanumericos Fijo
      *                y S1CLIOK es de 55 caracteres alfanumericos Fijo
       DATA DIVISION.
       FILE SECTION.
       FD  E1CLIEN
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.

       01  REG-E1CLIEN              PIC X(50)
           .

      * 5 caracteres de contador secuencial + 50 caracteres de la entrad
       FD  S1CLIOK
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.

       01  REG-S1CLIOK              PIC X(55)
           .

      **************************************
       WORKING-STORAGE SECTION.
      **************************************
       77  FILLER                   PIC X(26)      VALUE
            '* INICIO WORKING-STORAGE *'
           .

      *****  FILE STATUS  ******************
      * Variables establecidas arriba para validar estado de archivos
       77  FS-E1CLIEN               PIC X(02)
           .
           88  FS-E1-OK                            VALUE '00'
           .
           88  FS-E1-FIN-LECTURA                   VALUE '10'
           .
           88  FS-E1-ERROR                         VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

       77  FS-S1CLIOK               PIC X(02)
           .
           88  FS-S1-OK                            VALUE '00'
           .
           88  FS-S1-FIN-LECTURA                   VALUE '10'
           .
           88  FS-S1-ERROR                         VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

      *****  COPY DE ENTRADA  ***********************
           COPY CPNOVCLI
           .

      *****  COPY DE SALIDA  ***********************
           COPY CPNCLIV
           .
      ********  CONDICIONES A CUMPLIR PARA TIP-DOC  ********
       77  WS-CLI-DU                PIC X(2)       VALUE 'DU'
           .
       77  WS-CLI-PA                PIC X(2)       VALUE 'PA'
           .
       77  WS-CLI-PE                PIC X(2)       VALUE 'PE'
           .
       77  WS-CLI-CI                PIC X(2)       VALUE 'CI'
           .

      ********  FECHA DEL SISTEMA ***************
       77  WS-FECHA                 PIC 9(06)      VALUE ZEROS
           .

      ********  BANDERA PARA CAMPO ERRONEO      ************
       01  WS-CAMPO                 PIC X
           .
           88  WS-CAMP-ERR-SI                      VALUE 'S'
           .
           88  WS-CAMP-ERR-NO                      VALUE 'N'
           .

      ********  BANDERA PARA A¨O BISIESTO        ************
       01  WS-ANIO-BISIESTO         PIC X
           .
           88  WS-SI-BISIESTO                      VALUE 'S'
           .
           88  WS-NO-BISIESTO                      VALUE 'N'
           .

      ******** FECHA INTERNA PARA COMPARACION    **********
       01  WS-NOV-CLI-FECHA
           .
           03  WS-FEC-CLI-AAAA      PIC 9(04)      VALUE ZEROS
           .
           03  WS-FEC-CLI-MM        PIC 9(02)      VALUE ZEROS
           .
           03  WS-FEC-CLI-DD        PIC 9(02)      VALUE ZEROS
           .

      ******* A¨O LIMITE ACEPTADO  ****************
       77  WS-FEC-CLI-LIMITE        PIC 9(04)      VALUE 2025
           .

      ******* CALCULO DEL A¨O BISIESTO *********
       77  WS-COCIENTE              PIC 9(04)
           .
       77  WS-RESTO400              PIC 9(04)
           .
       77  WS-RESTO4                PIC 9(04)
           .
       77  WS-RESTO100              PIC 9(04)
           .

      ******** VARIABLES PARA DISPLAY  *******
       01  MUESTRA-TXT.
           03 D-LINEA               PIC X(56)        VALUE
            "********************************************************"
           .
           03 D-TIP-DOC             PIC X(09)        VALUE "TIP-DOC '"
           .
           03 D-NRO-DOC             PIC X(15)        VALUE
            "' CON NRO-DOC: "
           .
           03 D-CONTEXT             PIC X(19)        VALUE
            " CONTIENE EL ERROR:"
           .
           03 D-TIP-DOC-I           PIC X(21)        VALUE
            "   TIP-DOC INVALIDO -"
           .
           03 D-TIP-DOC-V           PIC X(46)        VALUE
            " SOLO SE ADMITE TIP-DOC 'DU','PA','PE' O 'CI'."
           .
           03 D-NRO-DOC-I           PIC X(24)        VALUE
            " - NRO-DOC NO NUMERICO -"
           .
           03 D-NRO-SUC             PIC X(13)        VALUE
            "   NRO-SUCUR "
           .
           03 D-NO-NRIC             PIC X(13)        VALUE
            " NO NUMERICO."
           .
           03 D-TIP-CNT             PIC X(12)        VALUE
            "   TIP-CNT '"
           .
           03 D-TIP-CNT-I           PIC X(23)        VALUE
            "' NO ES '01'-'02'-'03'."
           .
           03 D-CLI-NRO             PIC X(11)        VALUE
            "   CLI-NRO "
           .
           03 D-CLI-IMP             PIC X(11)        VALUE "   CLI-IMP "
           .
           03 D-FECHA               PIC X(10)        VALUE "   FECHA '"
           .
           03 D-FECHA-I             PIC X(29)        VALUE
            "' ES MAYOR A LA FECHA ACTUAL."
           .
           03 D-FECHA-I2            PIC X(19)        VALUE
            "' ES MENOR AL 2025."
           .
      ****** CONTADOR DE TOTALES
       77  WS-CANT-LEIDOS           PIC 9(3)         VALUE ZEROS
           .
       77  WS-CANT-OK               PIC 9(3)         VALUE ZEROS
           .
       77  WS-CANT-ERROR            PIC 9(3)         VALUE ZEROS
           .


       77  FILLER                   PIC X(26)      VALUE
            '* FINAL  WORKING-STORAGE *'
           .

      ***************************************************************.
       PROCEDURE DIVISION.
      ***************
       MAIN-PROGRAM.
      ***************
           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    FS-E1-FIN-LECTURA
                OR    FS-E1-ERROR

           PERFORM    3000-FINAL
              THRU    3000-FINAL-F
           .

       MAIN-PROGRAM-F. GOBACK.
      ***************
       1000-INICIO.
      ***************
      * Apertura de archivo de entrada
           OPEN INPUT E1CLIEN

           ACCEPT WS-FECHA FROM DATE

      * Inicializar copy de entrada
           INITIALIZE WS-REG-NOVCLIE
      * Si hay error en la apertura de archivo, notificar
           IF         FS-E1-ERROR
              DISPLAY '* ERROR EN OPEN ENTRADA = '
                      FS-E1CLIEN
              MOVE    9999                         TO RETURN-CODE

      * Leer registro de archivo entrada si no hay error; notificar si
      * el archivo estuviera vacio.
           ELSE
              PERFORM 2100-LEER
                 THRU 2100-LEER-F
              IF      FS-E1-FIN-LECTURA
                 DISPLAY "Archivo vacio."
                 MOVE    9999                      TO RETURN-CODE

              END-IF
           END-IF

      *
      * Apertura de archivo de salida
           OPEN OUTPUT S1CLIOK

      * Si hay error en la apertura de archivo, notificar
           IF         FS-S1-ERROR
              DISPLAY '* ERROR EN OPEN SALIDA = '
                      FS-S1CLIOK
              MOVE    9999                         TO RETURN-CODE

           END-IF
           .

       1000-INICIO-F. EXIT.

      ***************
       2000-PROCESO.
      ***************

           IF         WS-CAMP-ERR-NO
              PERFORM 2200-GRABAR-OK
                 THRU 2200-GRABAR-OK-F
           ELSE
              ADD     1                            TO WS-CANT-ERROR
           END-IF

      * Luego de grabar, leer siguiente registro.
           PERFORM    2100-LEER
              THRU    2100-LEER-F
           .
       2000-PROCESO-F. EXIT.

      ***************
       2100-LEER.
      ***************
           READ       E1CLIEN                    INTO WS-REG-NOVCLIE

      * Aumentar en 1 al contador de leido si lectura es OK.
           EVALUATE   FS-E1CLIEN
           WHEN       '00'
              PERFORM 2150-EVALUA-CAMPOS
                 THRU 2150-EVALUA-CAMPOS-F
              ADD     1                            TO WS-CANT-LEIDOS

           WHEN       '10'
              CONTINUE

           WHEN       OTHER
              DISPLAY '* ERROR EN LECTURA ENTRADA= '
                      FS-E1CLIEN
              MOVE    9999                         TO RETURN-CODE

           END-EVALUATE
           .

       2100-LEER-F. EXIT.

      *********************
       2150-EVALUA-CAMPOS.
      *********************
      * Primero mandamos que no hay errores actualmente
           SET        WS-CAMP-ERR-NO               TO TRUE

      * Evaluamos si TIP-DOC es valido
           IF         NOV-TIP-DOC
           =         (WS-CLI-DU
           OR         WS-CLI-PA
           OR         WS-CLI-PE
           OR         WS-CLI-CI)
              CONTINUE

           ELSE
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-TIP-DOC-I
                      D-TIP-DOC-V
           END-IF

      * Evaluamos si NRO-DOC es valido
           IF         NOV-NRO-DOC              IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-NRO-DOC-I
           END-IF

      * Evaluamos si SUCUR es valido
           IF         NOV-SUC                  IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-NRO-SUC
                      NOV-SUC
                      D-NO-NRIC
           END-IF

      * Evaluamos si CLI-TIPO es valido
           IF         NOV-CLI-TIPO
           =         (01
           OR         02
           OR         03)
              CONTINUE

           ELSE
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-TIP-CNT
                      NOV-CLI-TIPO
                      D-TIP-CNT-I
           END-IF

      * Evaluamos si CLI-NRO es valido
           IF         NOV-CLI-NRO              IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-CLI-NRO
                      NOV-CLI-NRO
                      D-NO-NRIC
           END-IF

      * Evaluamos si CLI-IMPORTE es valido
           IF         NOV-CLI-IMP              IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F
              DISPLAY D-CLI-IMP
                      NOV-CLI-IMP
                      D-NO-NRIC
           END-IF

      * Mueve fecha de arch entrada a variable intera desglosada
           MOVE       NOV-CLI-FECHA                TO WS-NOV-CLI-FECHA

      * Evaluamos si FECHA es valida
           IF         WS-NOV-CLI-FECHA         IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-FECHA
                      WS-NOV-CLI-FECHA
                      D-NO-NRIC
           END-IF

      * Evaluamos si fecha de registro es mayor a fecha actual
           IF         WS-NOV-CLI-FECHA(03:06)
           >          WS-FECHA

              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-FECHA
                      WS-NOV-CLI-FECHA
                      D-FECHA-I
           END-IF

      * Evaluamos si aão de registro es mayor al 2025
           IF         WS-FEC-CLI-AAAA
           <          WS-FEC-CLI-LIMITE
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-FECHA
                      WS-NOV-CLI-FECHA
                      D-FECHA-I2
           END-IF

      * Evaluamos si mes de registro es alguno de los siguientes, ademas
      * de evaluar si es mayor a 30 dias
           IF         WS-FEC-CLI-MM
           =         (04
           OR         06
           OR         09
           OR         11)
              IF      WS-FEC-CLI-DD
              >       30
                 PERFORM 2151-DISPLAY-ERROR
                    THRU 2151-DISPLAY-ERROR-F
                 DISPLAY D-FECHA
                         NOV-CLI-FECHA
                         "' EXCEDE LOS 30 DIAS DEL MES '"
                         WS-FEC-CLI-MM
                         "'."
              END-IF
           END-IF

      * Evaluamos si mes de registro es alguno de los siguientes, ademas
      * de evaluar si es mayor a 31 dias
           IF          WS-FEC-CLI-MM
           =          (01
           OR          03
           OR          05
           OR          07
           OR          08
           OR          10
           OR          12)
              IF       WS-FEC-CLI-DD
              >        31
                 PERFORM 2151-DISPLAY-ERROR
                    THRU 2151-DISPLAY-ERROR-F
                 DISPLAY D-FECHA
                         NOV-CLI-FECHA
                         "' EXCEDE LOS 31 DIAS DEL MES '"
                         WS-FEC-CLI-MM
                         "'."
              END-IF
           END-IF

      * Vamos a calular si aão es bisiesto
           PERFORM     2155-CALCULA-BISIESTO
              THRU     2155-CALCULA-BISIESTO-F

      * ERROR de ser bisiesto y excede de 29 dias
           IF          WS-SI-BISIESTO
              IF       WS-FEC-CLI-MM
              =        02
                 IF    WS-FEC-CLI-DD
                 >     29
                    PERFORM 2151-DISPLAY-ERROR
                       THRU 2151-DISPLAY-ERROR-F
                    DISPLAY D-FECHA NOV-CLI-FECHA
                            "' EXCEDE LOS 29 DIAS DEL MES '"
                            WS-FEC-CLI-MM
                            "'."
                 END-IF
              END-IF
      * ERROR de no ser bisiesto y excede de 28 dias
           ELSE
              IF       WS-FEC-CLI-MM
              =        02
                 IF    WS-FEC-CLI-DD
                 >     28
                    PERFORM 2151-DISPLAY-ERROR
                       THRU 2151-DISPLAY-ERROR-F
                    DISPLAY D-FECHA
                            NOV-CLI-FECHA
                            "' EXCEDE LOS 28 DIAS DEL MES '"
                            WS-FEC-CLI-MM
                            "'."
                 END-IF
              END-IF
           END-IF
           .
       2150-EVALUA-CAMPOS-F. EXIT.

      *********************
       2151-DISPLAY-ERROR.
      *********************
      *  MUESTRA SALIDA DE ERRORES
           SET         WS-CAMP-ERR-SI              TO TRUE

           DISPLAY     D-LINEA

           DISPLAY     D-TIP-DOC
                       NOV-TIP-DOC
                       D-NRO-DOC
                       NOV-NRO-DOC

           DISPLAY     D-CONTEXT
           .

       2151-DISPLAY-ERROR-F. EXIT.

      ************************
       2155-CALCULA-BISIESTO.
      ************************

           DIVIDE      WS-FEC-CLI-AAAA
           BY          400
           GIVING      WS-COCIENTE
           REMAINDER   WS-RESTO400

           DIVIDE      WS-FEC-CLI-AAAA
           BY          100
           GIVING      WS-COCIENTE
           REMAINDER   WS-RESTO100

           DIVIDE      WS-FEC-CLI-AAAA
           BY          4
           GIVING      WS-COCIENTE
           REMAINDER   WS-RESTO4

           IF          WS-RESTO400
           =           0
              SET      WS-SI-BISIESTO              TO TRUE

           ELSE
              IF      (WS-RESTO4
              =        0
              AND      WS-RESTO100
              NOT
              =        0)
                 SET   WS-SI-BISIESTO              TO TRUE

              ELSE
                 SET   WS-NO-BISIESTO              TO TRUE
              END-IF
           END-IF
           .

       2155-CALCULA-BISIESTO-F. EXIT.

      *****************
       2200-GRABAR-OK.
      *****************
      * Aumentar en 1 al contador de registros que cumplen filtro
           ADD        1                            TO WS-CANT-OK

      * Mover cantidad a valor secuencial de arch salida
           MOVE       WS-CANT-OK                   TO NOV-SECUEN

      * Mover valor de entrada a lo restante del archivo salida
           MOVE       WS-REG-NOVCLIE               TO NOV-RESTO

      * Escribir salida
           WRITE      REG-S1CLIOK                FROM REG-NOVCLIE-VAL

           EVALUATE   FS-S1CLIOK
           WHEN       '00'
           WHEN       '10'
              CONTINUE

           WHEN       OTHER
              DISPLAY '* ERROR EN GRABAR SALIDA = '
                      FS-S1CLIOK
              MOVE    9999                         TO RETURN-CODE
           END-EVALUATE
           .

       2200-GRABAR-OK-F. EXIT.

      **************
       3000-FINAL.
      **************
           IF         RETURN-CODE            NOT EQUAL 9999
              PERFORM 3010-CLOSE-FILES
                 THRU 3010-CLOSE-FILES-F
              PERFORM 3020-MOSTRAR-TOTALES
                 THRU 3020-MOSTRAR-TOTALES-F
           END-IF
           .

       3000-FINAL-F. EXIT.

      ********************
       3010-CLOSE-FILES.
      ********************
           CLOSE      E1CLIEN
           IF         FS-E1-ERROR
              DISPLAY '* ERROR EN CLOSE ENTRADA= '
                      FS-E1CLIEN
              MOVE    9999                          TO RETURN-CODE
           END-IF

           CLOSE S1CLIOK
           IF         FS-S1-ERROR
              DISPLAY '* ERROR EN CLOSE SALIDA= '
                      FS-S1CLIOK
              MOVE    9999                          TO RETURN-CODE
           END-IF
           .

       3010-CLOSE-FILES-F. EXIT.

      ************************
       3020-MOSTRAR-TOTALES.
      ************************
           DISPLAY    "-----------------------------------------"
           DISPLAY    ' TOTAL DE ENTRADAS LEIDAS: '
                      WS-CANT-LEIDOS
           DISPLAY    ' TOTAL DE APROBADOS      : '
                      WS-CANT-OK
           DISPLAY    ' TOTAL DE ERR‰NEOS       : '
                      WS-CANT-ERROR
           .

       3020-MOSTRAR-TOTALES-F. EXIT.
