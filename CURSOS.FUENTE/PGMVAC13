       IDENTIFICATION DIVISION.
       PROGRAM-ID PGMVAC13.
      ****************************************************************
      *  PROGRAMA   : PGMVAC13                                      *
      *  AUTOR      : (Nombre del autor)                            *
      *  FECHA      : 15-04-2025                                    *
      *  DESCRIPCION: AsincrÏnico 08 - ValidaciÏn de todos los      *
      *               campos del archivo de entrada. Si todos los   *
      *               filtros se cumplen, mueve el registro a       *
      *               salida y cuenta los aprobados.                *
      ****************************************************************
      *  JCL EJEC   : JCLEJEM1                                      *
      *  ENTRADA    : KC03C12.FIX.SINCR14.PGMVAC12.E1.NOVCLIEN      *
      *               E1CLIEN (entrada, 50 bytes, formato fijo)     *
      *  SALIDA     : KC03C12.FIX.ASINCR08.PGMVAC13.S1.NOVCLIEN     *
      *               S1CLIOK (salida, 55 bytes, formato fijo)      *
      *  COPYS      : CPNOVCLI (entrada), CPNCLIV (salida)          *
      ****************************************************************
      *  MANTENIMIENTO                                              *
      *  FECHA     *  DETALLE              *  COD                   *
      * 15-04-2025 *  CreaciÏn             *  001                   *
      * 10-07-2025 *  ReestructuraciÏn     *  002                   *
      *            *  profesional y comentarios enriquecidos        *
      ****************************************************************
      *  DETALLES TECNICOS DE LAS COPYS                            *
      *  - COPY CPNOVCLI (entrada):                                *
      *    01 WS-REG-NOVCLIE.                                      *
      *       05 NOV-TIP-DOC   PIC X(2)   --> Tipo de documento    *
      *          Valores: 'DU'=Documento ßnico, 'PA'=Pasaporte,    *
      *          'PE'=Permiso, 'CI'=C©dula Identidad.              *
      *       05 NOV-NRO-DOC   PIC X(10)  --> N£mero de documento  *
      *       05 NOV-SUC       PIC X(5)   --> Sucursal, num©rica   *
      *       05 NOV-CLI-TIPO  PIC 9(2)   --> Tipo cliente         *
      *       05 NOV-CLI-NRO   PIC X(10)  --> N£mero cliente       *
      *       05 NOV-CLI-IMP   PIC 9(8)V99--> Importe              *
      *       05 NOV-CLI-FECHA PIC 9(8)   --> Fecha (AAAAMMDD)     *
      *       ... otros campos seg£n layout de la copy.            *
      *  - COPY CPNCLIV (salida):                                  *
      *    01 REG-NOVCLIE-VAL.                                     *
      *       05 NOV-SECUEN    PIC 9(3)   --> Secuencial salida    *
      *       05 NOV-RESTO     PIC X(50)  --> Resto de datos       *
      *  - Uso en el programa:                                     *
      *    - Se valida cada campo del registro de entrada.         *
      *    - Si todos los filtros se cumplen, se mueve el registro *
      *      a salida y se incrementa el contador de aprobados.    *
      *    - Si alg£n campo falla, se muestra mensaje de error.    *
      *    - Se acumulan y muestran totales de le•dos, aprobados y *
      *      errÏneos.                                             *
      ****************************************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
      * E1CLIEN es la variable interna del archivo de entrada y se
      * asigna al DDCLIEN que es variable externa establecida en el JOB

      * S1CLIOK es la variable interna del archivo de salida y se
      * asigna al DDCLIOK que es variable externa establecida en el JOB

      * El FS es una validador interno del estado del archivo asignado
       FILE-CONTROL.
           SELECT         E1CLIEN
           ASSIGN         DDCLIEN
           FILE STATUS IS FS-E1CLIEN
           .

           SELECT         S1CLIOK
           ASSIGN         DDCLIOK
           FILE STATUS IS FS-S1CLIOK
           .

      * Establecemos que E1CLIEN es de 50 caracteres alfanumericos Fijo
      *                y S1CLIOK es de 55 caracteres alfanumericos Fijo
       DATA DIVISION.
       FILE SECTION.
       FD  E1CLIEN
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.

       01  REG-E1CLIEN              PIC X(50)
           .

      * 5 caracteres de contador secuencial + 50 caracteres de la entrad
       FD  S1CLIOK
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.

       01  REG-S1CLIOK              PIC X(55)
           .

      **************************************
       WORKING-STORAGE SECTION.
      **************************************
       77  FILLER                   PIC X(26)      VALUE
            '* INICIO WORKING-STORAGE *'
           .

      *****  FILE STATUS  ******************
      * Variables establecidas arriba para validar estado de archivos
       77  FS-E1CLIEN               PIC X(02)
           .
           88  FS-E1-OK                            VALUE '00'
           .
           88  FS-E1-FIN-LECTURA                   VALUE '10'
           .
           88  FS-E1-ERROR                         VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

       77  FS-S1CLIOK               PIC X(02)
           .
           88  FS-S1-OK                            VALUE '00'
           .
           88  FS-S1-FIN-LECTURA                   VALUE '10'
           .
           88  FS-S1-ERROR                         VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

      *****  COPY DE ENTRADA  ***********************
           COPY CPNOVCLI
           .

      *****  COPY DE SALIDA  ***********************
           COPY CPNCLIV
           .
      ********  CONDICIONES A CUMPLIR PARA TIP-DOC  ********
       77  WS-CLI-DU                PIC X(2)       VALUE 'DU'
           .
       77  WS-CLI-PA                PIC X(2)       VALUE 'PA'
           .
       77  WS-CLI-PE                PIC X(2)       VALUE 'PE'
           .
       77  WS-CLI-CI                PIC X(2)       VALUE 'CI'
           .

      ********  FECHA DEL SISTEMA ***************
       77  WS-FECHA                 PIC 9(06)      VALUE ZEROS
           .

      ********  BANDERA PARA CAMPO ERRONEO      ************
       01  WS-CAMPO                 PIC X
           .
           88  WS-CAMP-ERR-SI                      VALUE 'S'
           .
           88  WS-CAMP-ERR-NO                      VALUE 'N'
           .

      ********  BANDERA PARA A¨O BISIESTO        ************
       01  WS-ANIO-BISIESTO         PIC X
           .
           88  WS-SI-BISIESTO                      VALUE 'S'
           .
           88  WS-NO-BISIESTO                      VALUE 'N'
           .

      ******** FECHA INTERNA PARA COMPARACION    **********
       01  WS-NOV-CLI-FECHA
           .
           03  WS-FEC-CLI-AAAA      PIC 9(04)      VALUE ZEROS
           .
           03  WS-FEC-CLI-MM        PIC 9(02)      VALUE ZEROS
           .
           03  WS-FEC-CLI-DD        PIC 9(02)      VALUE ZEROS
           .

      ******* A¨O LIMITE ACEPTADO  ****************
       77  WS-FEC-CLI-LIMITE        PIC 9(04)      VALUE 2025
           .

      ******* CALCULO DEL A¨O BISIESTO *********
       77  WS-COCIENTE              PIC 9(04)
           .
       77  WS-RESTO400              PIC 9(04)
           .
       77  WS-RESTO4                PIC 9(04)
           .
       77  WS-RESTO100              PIC 9(04)
           .

      ******** VARIABLES PARA DISPLAY  *******
       01  MUESTRA-TXT.
           03 D-LINEA               PIC X(56)        VALUE
            "********************************************************"
           .
           03 D-TIP-DOC             PIC X(09)        VALUE "TIP-DOC '"
           .
           03 D-NRO-DOC             PIC X(15)        VALUE
            "' CON NRO-DOC: "
           .
           03 D-CONTEXT             PIC X(19)        VALUE
            " CONTIENE EL ERROR:"
           .
           03 D-TIP-DOC-I           PIC X(21)        VALUE
            "   TIP-DOC INVALIDO -"
           .
           03 D-TIP-DOC-V           PIC X(46)        VALUE
            " SOLO SE ADMITE TIP-DOC 'DU','PA','PE' O 'CI'."
           .
           03 D-NRO-DOC-I           PIC X(24)        VALUE
            " - NRO-DOC NO NUMERICO -"
           .
           03 D-NRO-SUC             PIC X(13)        VALUE
            "   NRO-SUCUR "
           .
           03 D-NO-NRIC             PIC X(13)        VALUE
            " NO NUMERICO."
           .
           03 D-TIP-CNT             PIC X(12)        VALUE
            "   TIP-CNT '"
           .
           03 D-TIP-CNT-I           PIC X(23)        VALUE
            "' NO ES '01'-'02'-'03'."
           .
           03 D-CLI-NRO             PIC X(11)        VALUE
            "   CLI-NRO "
           .
           03 D-CLI-IMP             PIC X(11)        VALUE "   CLI-IMP "
           .
           03 D-FECHA               PIC X(10)        VALUE "   FECHA '"
           .
           03 D-FECHA-I             PIC X(29)        VALUE
            "' ES MAYOR A LA FECHA ACTUAL."
           .
           03 D-FECHA-I2            PIC X(19)        VALUE
            "' ES MENOR AL 2025."
           .
      ****** CONTADOR DE TOTALES
       77  WS-CANT-LEIDOS           PIC 9(3)         VALUE ZEROS
           .
       77  WS-CANT-OK               PIC 9(3)         VALUE ZEROS
           .
       77  WS-CANT-ERROR            PIC 9(3)         VALUE ZEROS
           .


       77  FILLER                   PIC X(26)      VALUE
            '* FINAL  WORKING-STORAGE *'
           .

      ***************************************************************.
       PROCEDURE DIVISION.
      ***************
       MAIN-PROGRAM.
      ***************
           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    FS-E1-FIN-LECTURA
                OR    FS-E1-ERROR

           PERFORM    3000-FINAL
              THRU    3000-FINAL-F
           .

       MAIN-PROGRAM-F. GOBACK.
      ***************
       1000-INICIO.
      ***************
      * Apertura de archivo de entrada
           OPEN INPUT E1CLIEN

           ACCEPT WS-FECHA FROM DATE

      * Inicializar copy de entrada
           INITIALIZE WS-REG-NOVCLIE
      * Si hay error en la apertura de archivo, notificar
           IF         FS-E1-ERROR
              DISPLAY '* ERROR EN OPEN ENTRADA = '
                      FS-E1CLIEN
              MOVE    9999                         TO RETURN-CODE

      * Leer registro de archivo entrada si no hay error; notificar si
      * el archivo estuviera vacio.
           ELSE
              PERFORM 2100-LEER
                 THRU 2100-LEER-F

              IF      FS-E1-FIN-LECTURA
                 DISPLAY "Archivo vacio."

                 MOVE    9999                      TO RETURN-CODE
              END-IF
           END-IF

      *
      * Apertura de archivo de salida
           OPEN OUTPUT S1CLIOK

      * Si hay error en la apertura de archivo, notificar
           IF         FS-S1-ERROR
              DISPLAY '* ERROR EN OPEN SALIDA = '
                      FS-S1CLIOK

              MOVE    9999                         TO RETURN-CODE
           END-IF
           .

       1000-INICIO-F. EXIT.

      ***************
       2000-PROCESO.
      ***************
      * Proceso principal: Si la bandera WS-CAMP-ERR-NO estê en TRUE,
      * significa que el registro cumple todos los filtros y puede ser
      * grabado en el archivo de salida. Si hay error en alg£n campo,
      * se suma al contador de errÏneos. Luego se lee el siguiente registro.
      * Contadores:
      * -WS-CAMP-ERR-NO/WS-CAMP-ERR-SI: variables 88 para control de error.
      * -WS-CANT-OK: contador de registros vêlidos y grabados.
      * -WS-CANT-ERROR: contador de registros con error en alg£n campo.
      * -WS-CANT-LEIDOS: contador de registros le•dos.
      *
           IF         WS-CAMP-ERR-NO
      * Si no hay error, grabar registro en salida y sumar aprobados.
              PERFORM 2200-GRABAR-OK
                 THRU 2200-GRABAR-OK-F
           ELSE
      * Si hay error en alg£n campo, sumar al contador de errÏneos.
              ADD     1                            TO WS-CANT-ERROR
           END-IF

      * Luego de grabar o sumar error, leer siguiente registro de entrada.
           PERFORM    2100-LEER
              THRU    2100-LEER-F
           .
       2000-PROCESO-F. EXIT.

      ***************
       2100-LEER.
      ***************
      * Lee un registro del archivo de entrada (E1CLIEN) y lo mueve
      * al buffer WS-REG-NOVCLIE (COPY CPNOVCLI). Eval£a el file status
      * FS-E1CLIEN para determinar si la lectura fue exitosa, fin de archivo
      * o error. Si es exitosa, llama a 2150-EVALUA-CAMPOS para validar
      * todos los campos y suma al contador de le•dos.
      * Si hay error, muestra mensaje y termina con cÏdigo 9999.
      *
           READ       E1CLIEN                    INTO WS-REG-NOVCLIE

           EVALUATE   TRUE
               WHEN   FS-E1-OK
      * Lectura exitosa: validar campos y sumar al contador de le•dos.
                  PERFORM 2150-EVALUA-CAMPOS
                     THRU 2150-EVALUA-CAMPOS-F

                  ADD     1                    TO WS-CANT-LEIDOS
               WHEN   FS-E1-FIN-LECTURA
      * Fin de archivo: continuar sin acciÏn.
                  CONTINUE
               WHEN   FS-E1-ERROR
      * Error en lectura: mostrar mensaje y terminar.
                  DISPLAY '* ERROR EN LECTURA ENTRADA= '
                          FS-E1CLIEN

                  MOVE    9999                 TO RETURN-CODE
           END-EVALUATE
           .
       2100-LEER-F. EXIT.

      *********************
       2150-EVALUA-CAMPOS.
      *********************
      * Valida todos los campos del registro de entrada seg£n reglas
      * de negocio y formato. Si alg£n campo no cumple, se llama a
      * 2151-DISPLAY-ERROR y se setea la bandera WS-CAMP-ERR-SI.
      * Si todos cumplen, la bandera queda en WS-CAMP-ERR-NO y el
      * registro serê grabado en salida.
      *
      * Validaciones:
      * - NOV-TIP-DOC: debe ser 'DU', 'PA', 'PE', 'CI'.
      * - NOV-NRO-DOC: debe ser num©rico.
      * - NOV-SUC: debe ser num©rico.
      * - NOV-CLI-TIPO: debe ser 01, 02 o 03.
      * - NOV-CLI-NRO: debe ser num©rico.
      * - NOV-CLI-IMP: debe ser num©rico.
      * - NOV-CLI-FECHA: debe ser num©rica, no mayor a la fecha actual,
      *   aão mayor o igual a 2025, mes y d•a vêlidos seg£n calendario.
      *
           SET        WS-CAMP-ERR-NO               TO TRUE

      * ValidaciÏn de tipo de documento
           IF         NOV-TIP-DOC
           =         (WS-CLI-DU
           OR         WS-CLI-PA
           OR         WS-CLI-PE
           OR         WS-CLI-CI)
              CONTINUE

           ELSE
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-TIP-DOC-I
                      D-TIP-DOC-V
           END-IF

      * ValidaciÏn de n£mero de documento
           IF         NOV-NRO-DOC              IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-NRO-DOC-I
           END-IF

      * ValidaciÏn de sucursal
           IF         NOV-SUC                  IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-NRO-SUC
                      NOV-SUC
                      D-NO-NRIC
           END-IF

      * ValidaciÏn de tipo de cliente
           IF         NOV-CLI-TIPO
           =         (01
           OR         02
           OR         03)
              CONTINUE

           ELSE
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-TIP-CNT
                      NOV-CLI-TIPO
                      D-TIP-CNT-I
           END-IF

      * ValidaciÏn de n£mero de cliente
           IF         NOV-CLI-NRO              IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-CLI-NRO
                      NOV-CLI-NRO
                      D-NO-NRIC
           END-IF

      * ValidaciÏn de importe
           IF         NOV-CLI-IMP              IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-CLI-IMP
                      NOV-CLI-IMP
                      D-NO-NRIC
           END-IF

      * Mueve fecha de entrada a variable interna desglosada
           MOVE       NOV-CLI-FECHA                TO WS-NOV-CLI-FECHA

      * ValidaciÏn de fecha: formato, aão, mes y d•a
           IF         WS-NOV-CLI-FECHA         IS NOT NUMERIC
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-FECHA
                      WS-NOV-CLI-FECHA
                      D-NO-NRIC
           END-IF

      * Evaluamos si fecha de registro es mayor a fecha actual
           IF         WS-NOV-CLI-FECHA(03:06)
           >          WS-FECHA

              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-FECHA
                      WS-NOV-CLI-FECHA
                      D-FECHA-I
           END-IF

      * Evaluamos si aão de registro es mayor al 2025
           IF         WS-FEC-CLI-AAAA
           <          WS-FEC-CLI-LIMITE
              PERFORM 2151-DISPLAY-ERROR
                 THRU 2151-DISPLAY-ERROR-F

              DISPLAY D-FECHA
                      WS-NOV-CLI-FECHA
                      D-FECHA-I2
           END-IF

      * ValidaciÏn de mes y d•a seg£n reglas de calendario
           EVALUATE   WS-FEC-CLI-MM
               WHEN   04
               WHEN   06
               WHEN   09
               WHEN   11
                  IF      WS-FEC-CLI-DD > 30
                      PERFORM 2151-DISPLAY-ERROR
                         THRU 2151-DISPLAY-ERROR-F

                      DISPLAY D-FECHA
                              NOV-CLI-FECHA
                              "' EXCEDE LOS 30 DIAS DEL MES '"
                              WS-FEC-CLI-MM
                              "'."
                  END-IF
               WHEN   01
               WHEN   03
               WHEN   05
               WHEN   07
               WHEN   08
               WHEN   10
               WHEN   12
                  IF      WS-FEC-CLI-DD > 31
                      PERFORM 2151-DISPLAY-ERROR
                         THRU 2151-DISPLAY-ERROR-F

                      DISPLAY D-FECHA
                              NOV-CLI-FECHA
                              "' EXCEDE LOS 31 DIAS DEL MES '"
                              WS-FEC-CLI-MM
                              "'."
                  END-IF
               WHEN   02
      * ValidaciÏn especial para febrero y aão bisiesto
                  PERFORM     2155-CALCULA-BISIESTO
                     THRU     2155-CALCULA-BISIESTO-F

                  IF      WS-SI-BISIESTO
                      IF  WS-FEC-CLI-DD > 29
                          PERFORM 2151-DISPLAY-ERROR
                             THRU 2151-DISPLAY-ERROR-F

                          DISPLAY D-FECHA NOV-CLI-FECHA
                                  "' EXCEDE LOS 29 DIAS DEL MES '"
                                  WS-FEC-CLI-MM
                                  "'."
                      END-IF
                  ELSE
                      IF  WS-FEC-CLI-DD > 28
                          PERFORM 2151-DISPLAY-ERROR
                             THRU 2151-DISPLAY-ERROR-F

                          DISPLAY D-FECHA NOV-CLI-FECHA
                                  "' EXCEDE LOS 28 DIAS DEL MES '"
                                  WS-FEC-CLI-MM
                                  "'."
                      END-IF
                  END-IF
               WHEN OTHER
                  CONTINUE
           END-EVALUATE
           .

       2150-EVALUA-CAMPOS-F. EXIT.

      *********************
       2151-DISPLAY-ERROR.
      *********************
      *  MUESTRA SALIDA DE ERRORES
           SET         WS-CAMP-ERR-SI              TO TRUE

           DISPLAY     D-LINEA

           DISPLAY     D-TIP-DOC
                       NOV-TIP-DOC
                       D-NRO-DOC
                       NOV-NRO-DOC

           DISPLAY     D-CONTEXT
           .

       2151-DISPLAY-ERROR-F. EXIT.

      ************************
       2155-CALCULA-BISIESTO.
      ************************

           DIVIDE      WS-FEC-CLI-AAAA
           BY          400
           GIVING      WS-COCIENTE
           REMAINDER   WS-RESTO400

           DIVIDE      WS-FEC-CLI-AAAA
           BY          100
           GIVING      WS-COCIENTE
           REMAINDER   WS-RESTO100

           DIVIDE      WS-FEC-CLI-AAAA
           BY          4
           GIVING      WS-COCIENTE
           REMAINDER   WS-RESTO4

           IF          WS-RESTO400
           =           0
              SET      WS-SI-BISIESTO              TO TRUE

           ELSE
              IF      (WS-RESTO4
              =        0
              AND      WS-RESTO100
              NOT =    0)
                 SET   WS-SI-BISIESTO              TO TRUE

              ELSE
                 SET   WS-NO-BISIESTO              TO TRUE
              END-IF
           END-IF
           .

       2155-CALCULA-BISIESTO-F. EXIT.

      *****************
       2200-GRABAR-OK.
      *****************
      * Aumentar en 1 al contador de registros que cumplen filtro
           ADD        1                            TO WS-CANT-OK

      * Mover cantidad a valor secuencial de arch salida
           MOVE       WS-CANT-OK                   TO NOV-SECUEN

      * Mover valor de entrada a lo restante del archivo salida
           MOVE       WS-REG-NOVCLIE               TO NOV-RESTO

      * Escribir salida
           WRITE      REG-S1CLIOK                FROM REG-NOVCLIE-VAL

           IF         FS-S1-ERROR
              DISPLAY '* ERROR EN GRABAR SALIDA = '
                      FS-S1CLIOK

              MOVE    9999                         TO RETURN-CODE
           END-IF
           .

       2200-GRABAR-OK-F. EXIT.

      **************
       3000-FINAL.
      **************
           IF         RETURN-CODE            NOT EQUAL 9999
              PERFORM 3010-CLOSE-FILES
                 THRU 3010-CLOSE-FILES-F

              PERFORM 3020-MOSTRAR-TOTALES
                 THRU 3020-MOSTRAR-TOTALES-F
           END-IF
           .

       3000-FINAL-F. EXIT.

      ********************
       3010-CLOSE-FILES.
      ********************
           CLOSE      E1CLIEN
           IF         FS-E1-ERROR
              DISPLAY '* ERROR EN CLOSE ENTRADA= '
                      FS-E1CLIEN

              MOVE    9999                          TO RETURN-CODE
           END-IF

           CLOSE S1CLIOK
           IF         FS-S1-ERROR
              DISPLAY '* ERROR EN CLOSE SALIDA= '
                      FS-S1CLIOK

              MOVE    9999                          TO RETURN-CODE
           END-IF
           .

       3010-CLOSE-FILES-F. EXIT.

      ************************
       3020-MOSTRAR-TOTALES.
      ************************
           DISPLAY    "-----------------------------------------"

           DISPLAY    ' TOTAL DE ENTRADAS LEIDAS: '
                      WS-CANT-LEIDOS

           DISPLAY    ' TOTAL DE APROBADOS      : '
                      WS-CANT-OK

           DISPLAY    ' TOTAL DE ERR‰NEOS       : '
                      WS-CANT-ERROR
           .

       3020-MOSTRAR-TOTALES-F. EXIT.
