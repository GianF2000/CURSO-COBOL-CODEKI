       IDENTIFICATION DIVISION.
       PROGRAM-ID. PGMCORT5.

      ****************************************************************
      *  PROGRAMA   : PGMCORT5                                      *
      *  AUTOR      : (Nombre del autor)                            *
      *  FECHA      : 15-04-2025                                    *
      *  DESCRIPCION: AsincrÏnico 07 - Doble corte de control por   *
      *               Tipo de Documento y Sexo.                     *
      *               Lee archivo CLICOB y muestra acumulados por   *
      *               cada cambio de tip-doc y sexo.                *
      *               Prioridad: primero corte por SEXO, luego por  *
      *               TIP-DOC.                                      *
      *               Acumula y muestra totales por cada grupo.     *
      ****************************************************************
      *  JCL EJEC   : JCLCORT5                                      *
      *  ENTRADA    : KC03C12.FIX.SINCR132.PGMC1C12.CLICOB          *
      *               (entrada, 93 bytes, formato fijo)             *
      *  COPYS      : CLICOB (estructura de cliente, ver detalles   *
      *               t©cnicos abajo)                               *
      ****************************************************************
      *  MANTENIMIENTO                                              *
      *  FECHA     *  DETALLE              *  COD                   *
      * 15-04-2025 *  CreaciÏn             *  001                   *
      * 10-07-2025 *  ReestructuraciÏn     *  002                   *
      *            *  profesional y comentarios enriquecidos        *
      ****************************************************************
      *  DETALLES TECNICOS DE LA COPY CLICOB                       *
      *  - Estructura principal:                                   *
      *    01 WS-REG-CLICOB.                                       *
      *       05 WS-SUC-TIP-DOC   PIC X(2)   --> Tipo de documento *
      *          Valores: 'DU'=Documento ßnico, 'PA'=Pasaporte,    *
      *          'PE'=Permiso, 'CI'=C©dula Identidad.              *
      *       05 WS-SUC-SEXO      PIC X      --> Sexo              *
      *          Valores: 'F'=Femenino, 'M'=Masculino, 'O'=Otros.  *
      *       05 WS-SUC-NOMBRE    PIC X(30)  --> Nombre completo   *
      *       05 WS-SUC-DOMICILIO PIC X(30)  --> Domicilio         *
      *       05 WS-SUC-EST-CIV   PIC X(10)  --> Estado civil      *
      *       ... otros campos seg£n layout de la copy.            *
      *  - Uso en el programa:                                     *
      *    - Se filtra por WS-SUC-TIP-DOC y WS-SUC-SEXO para       *
      *      realizar cortes de control y acumulaciones.           *
      *    - Se muestran totales por cada cambio de tip-doc y sexo.*
      *    - WS-REG-CLICOB se utiliza como buffer de lectura.      *
      *  - Validaciones: Solo se procesan registros con valores    *
      *    vêlidos en tip-doc y sexo.                              *
      *  - Dependencias: COPY CLICOB debe estar actualizada y      *
      *    alineada con el layout de entrada.                      *
      ****************************************************************
      *  LOGICA DE DOBLE CORTE                                     *
      *  - Se realiza corte de control primero por SEXO (menor),    *
      *    luego por TIP-DOC (mayor).                              *
      *  - Cada vez que cambia el valor de sexo, se muestra el      *
      *    acumulado y se resetea el contador.                      *
      *  - Al cambiar tip-doc, se muestra el acumulado y se suma    *
      *    al total general.                                        *
      *  - Se utilizan acumuladores y mêscaras de ediciÏn para      *
      *    mostrar los totales.                                     *
      ****************************************************************

      ****************************************************************
      * ENVIRONMENT DIVISION                                         *
      ****************************************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
         SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      * Archivo de entrada: ENTRADA (CLICOB), asignado a DDENTRA     *
      * en el JCL. File status controlado por WS-ENT-CODE y 88.      *
       FILE-CONTROL.
           SELECT         ENTRADA
           ASSIGN         DDENTRA
           FILE STATUS IS WS-ENT-CODE
           .

      ****************************************************************
      * DATA DIVISION                                                *
      ****************************************************************
       DATA DIVISION.
       FILE SECTION.
      * ENTRADA: archivo de 93 caracteres, formato fijo.             *
       FD  ENTRADA
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           .

       01  REG-ENTRADA               PIC X(93)
           .

      ****************************************************************
      * WORKING-STORAGE SECTION                                      *
      ****************************************************************
       WORKING-STORAGE SECTION.

      * CÏdigos de estado para archivo ENTRADA:
      * 00 = OperaciÏn exitosa
      * 10 = Fin de archivo
      * 01-09, 11-99 = Error en operaciÏn
       77  WS-ENT-CODE              PIC X(02)
           .
           88  WS-ARCH-OK                          VALUE '00'
           .
           88  WS-ARCH-FIN-LECTURA                 VALUE '10'
           .
           88  WS-ARCH-ERROR                       VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

      * COPY CLICOB: incluye WS-SUC-TIP-DOC y WS-SUC-SEXO            *
           COPY CLICOB
           .

      * Constantes y textos para mostrar cortes                      *
       77  WS-CLIENTE-NOM-ANT       PIC X(18)      VALUE
           "TIPO DE DOCUMENTO "
           .
       77  WS-TOTAL-OK              PIC X(27)      VALUE
            'Total registros aprobados: '
           .
       77  WS-TOTAL-TOT             PIC X(24)      VALUE
           'TOTAL REGISTROS LEIDOS: '
           .

      * Variables para guardar tip-doc y sexo anterior               *
       77  WS-CLIENTE-DOC-ANT       PIC X(2)
           .
       77  WS-CLIENTE-SEXO-ANT      PIC X
           .

      * Condiciones a cumplir para tip-doc                           *
       77  WS-CLI-DU                PIC X(2)       VALUE 'DU'
           .
       77  WS-CLI-PA                PIC X(2)       VALUE 'PA'
           .
       77  WS-CLI-PE                PIC X(2)       VALUE 'PE'
           .
       77  WS-CLI-CI                PIC X(2)       VALUE 'CI'
           .

      * Condiciones a cumplir para sexo                              *
       77  WS-CLI-F                 PIC X          VALUE 'F'
           .
       77  WS-CLI-M                 PIC X          VALUE 'M'
           .
       77  WS-CLI-O                 PIC X          VALUE 'O'
           .

      * Totales y acumuladores                                      *
       77  WS-CANT-REG-SEXO-ANT     PIC 9(5)       VALUE ZEROS
           .
       77  WS-CANT-REG-DOC-ANT      PIC 9(5)       VALUE ZEROS
           .
       77  WS-CANT-REG-ANT-E        PIC ZZ.ZZ9     VALUE ZEROS
           .
       77  WS-CANT-REG-TOTAL-OK     PIC 9(6)       VALUE ZEROS
           .
       77  WS-CANT-REG-TOTAL        PIC 9(6)       VALUE ZEROS
           .
       77  WS-CANT-REG-TOT-E        PIC ZZZ.ZZ9    VALUE ZEROS
           .

      * **********************************************************
      * PROCEDURE DIVISION: LÏgica principal del programa         *
      * **********************************************************
       PROCEDURE DIVISION.

      * Controla el flujo principal del programa: inicializa,
      * procesa y finalizaa
      ***************
       MAIN-PROGRAM.

           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    WS-ARCH-FIN-LECTURA
                OR    WS-ARCH-ERROR

           PERFORM    9999-FINAL
              THRU    9999-FINAL-F
           .

       MAIN-PROGRAM-F. GOBACK.

      * **********************************************************
      * 1000-INICIO: InicializaciÏn y apertura de archivo         *
      * **********************************************************
       1000-INICIO.

           SET        WS-ARCH-OK                   TO TRUE

           OPEN INPUT ENTRADA

           IF NOT     WS-ARCH-OK
              DISPLAY '* ERROR EN OPEN ENTRADA = '
                      WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE
           END-IF

      * Primera lectura para corte de control                     *
           READ       ENTRADA
                      INTO WS-REG-CLICOB

           EVALUATE   WS-ENT-CODE
           WHEN       '00'
              ADD     1                          TO WS-CANT-REG-TOTAL-OK

      * Si cumple con las 2 condiciones, guarda valores de tipdoc y sexo
      * y aumenta en 1 la cantidad de registros de sexo
              IF      WS-SUC-TIP-DOC
              =      (WS-CLI-DU
              OR      WS-CLI-PA
              OR      WS-CLI-PE
              OR      WS-CLI-CI)
      *
                 IF   WS-SUC-SEXO
                 =   (WS-CLI-F
                 OR   WS-CLI-M
                 OR   WS-CLI-O)
                    MOVE    WS-SUC-TIP-DOC       TO WS-CLIENTE-DOC-ANT

                    MOVE    WS-SUC-SEXO          TO WS-CLIENTE-SEXO-ANT

                    ADD     1                    TO WS-CANT-REG-SEXO-ANT

      * Muestra cabecera de TIP-DOC
                    DISPLAY WS-CLIENTE-NOM-ANT
                            WS-CLIENTE-DOC-ANT
                 END-IF
              END-IF

           WHEN       '10'
              DISPLAY '* ARCHIVO ENTRADA VAC≤O      '
              MOVE    9999                       TO RETURN-CODE

           WHEN OTHER
              DISPLAY '* ERROR EN LECTURA ENTRADA = '
                      WS-ENT-CODE
              MOVE    9999                       TO RETURN-CODE

           END-EVALUATE

           .

       1000-INICIO-F. EXIT.

      * **********************************************************
      * 2000-PROCESO: Lectura y corte por cambios de tip-doc/sexo *
      * **********************************************************
       2000-PROCESO.

      * Leemos siguiente registro para evaluar cambios de tip-doc y sexo
           READ       ENTRADA
                      INTO WS-REG-CLICOB

           EVALUATE   WS-ENT-CODE

           WHEN       '00'
              ADD     1                          TO WS-CANT-REG-TOTAL-OK
      * Si cumple con las 2 condiciones, ir a la evaluaciÏn si
      * tip-doc y sexo
              IF      WS-SUC-TIP-DOC
              =      (WS-CLI-DU
              OR      WS-CLI-PA
              OR      WS-CLI-PE
              OR      WS-CLI-CI)
      *
                 IF   WS-SUC-SEXO
                 =   (WS-CLI-F
                 OR   WS-CLI-M
                 OR   WS-CLI-O)
                    PERFORM 2200-SEGUIR-PROCESO
                       THRU 2200-SEGUIR-PROCESO-F
                 END-IF
              END-IF

      * En caso de terminar lectura de archivo, hacer cortes de control
           WHEN '10'
              PERFORM 2100-CORTES-DE-CONTROLES
                 THRU 2100-CORTES-DE-CONTROLES-F

           WHEN OTHER
              DISPLAY '* ERROR EN LECTURA ENTRADA = '
                      WS-ENT-CODE
              MOVE    9999                       TO RETURN-CODE

           END-EVALUATE

           .

       2000-PROCESO-F. EXIT.

      * **********************************************************
      * 2100-CORTES-DE-CONTROLES: Corte por sexo y tip-doc        *
      * **********************************************************
       2100-CORTES-DE-CONTROLES.

      * Se realiza CC de SEXO primero por ser el campo "menor"    *
      * al TIP-DOC que es el "principal".                         *
      * Se muestra cantidad de registros acumulados del £ltimo     *
      * valor de SEXO y luego de TIP-DOC.                         *
           PERFORM    2235-CORTE-SEXO-CLIENTE
              THRU    2235-CORTE-SEXO-CLIENTE-F
           PERFORM    2230-CORTE-TIPO-DOC
              THRU    2230-CORTE-TIPO-DOC-F

           .

       2100-CORTES-DE-CONTROLES-F. EXIT.

      * **********************************************************
      * 2200-SEGUIR-PROCESO: AcumulaciÏn y corte por cambios      *
      * **********************************************************
       2200-SEGUIR-PROCESO.

      * Si no hay cambios en tip-doc ni sexo, aumenta en 1 a cantidad
      * de registros de sexo
           IF         WS-SUC-TIP-DOC          EQUAL WS-CLIENTE-DOC-ANT
              IF      WS-SUC-SEXO             EQUAL WS-CLIENTE-SEXO-ANT
                 ADD  1                          TO WS-CANT-REG-SEXO-ANT

      * En caso que haya cambio en campo SEXO, se hace corte de control
              ELSE
                 PERFORM 2235-CORTE-SEXO-CLIENTE
                    THRU 2235-CORTE-SEXO-CLIENTE-F
              END-IF

      * En caso haya cambio en TIP-DOC, se debe hacer CC primero al
      * SEXO y luego al TIP-DOC para mostrar el acumulado.         *
           ELSE
              PERFORM 2100-CORTES-DE-CONTROLES
              DISPLAY WS-CLIENTE-NOM-ANT
                      WS-CLIENTE-DOC-ANT
           END-IF

           .

       2200-SEGUIR-PROCESO-F. EXIT.

      * **********************************************************
      * 2230-CORTE-TIPO-DOC: Corte y muestra por tip-doc          *
      * **********************************************************
       2230-CORTE-TIPO-DOC.

      * Muevo acumulado de TIP-DOC a su mascara de ediciÏn         *
           MOVE       WS-CANT-REG-DOC-ANT        TO WS-CANT-REG-ANT-E

      * Muestra total de acumulados por TIP-DOC                    *
           DISPLAY    "Total de reg. de tip. docu "
                      WS-CANT-REG-ANT-E
           DISPLAY    "**********************************"

      * Guarda valor de nuevo TIP-DOC a variable interna           *
           MOVE       WS-SUC-TIP-DOC             TO WS-CLIENTE-DOC-ANT

      * Aãado acumulado de TIP-DOC al acumulado total del archivo  *
           ADD        WS-CANT-REG-DOC-ANT        TO WS-CANT-REG-TOTAL

      * Resetea el valor de los acumulados de TIP-DOC y la mascara *
           MOVE       ZEROS                      TO WS-CANT-REG-DOC-ANT
                                                    WS-CANT-REG-ANT-E

           .

       2230-CORTE-TIPO-DOC-F. EXIT.

      * **********************************************************
      * 2235-CORTE-SEXO-CLIENTE: Corte y muestra por sexo         *
      * **********************************************************
       2235-CORTE-SEXO-CLIENTE.

      * Muevo acumulado de SEXO a su mascara de ediciÏn            *
           MOVE       WS-CANT-REG-SEXO-ANT       TO WS-CANT-REG-ANT-E

      * Muestra texto y valor seg£n sexo                           *
           EVALUATE   WS-CLIENTE-SEXO-ANT

           WHEN       WS-CLI-F
              DISPLAY "   Femenino :"
                      WS-CANT-REG-ANT-E

           WHEN       WS-CLI-M
              DISPLAY "   Masculino:"
                      WS-CANT-REG-ANT-E

           WHEN       WS-CLI-O
              DISPLAY "   Otros    :"
                      WS-CANT-REG-ANT-E

           WHEN OTHER
              CONTINUE

           END-EVALUATE

      * Guarda valor de nueva SEXO a variable interna              *
           MOVE       WS-SUC-SEXO                TO WS-CLIENTE-SEXO-ANT

      * Aãado acumulado de SEXO al acumulado de TIP-DOC            *
           ADD        WS-CANT-REG-SEXO-ANT       TO WS-CANT-REG-DOC-ANT

      * Resetea el valor de los acumulados de SEXO y la mascara    *
           MOVE       ZEROS                      TO WS-CANT-REG-SEXO-ANT
                                                    WS-CANT-REG-ANT-E

      * Aumenta en 1 cantidad de registro de SEXO para nuevo valor *
           ADD        1                          TO WS-CANT-REG-SEXO-ANT

           .

       2235-CORTE-SEXO-CLIENTE-F. EXIT.

      * **********************************************************
      * 9999-FINAL: Mostrar totales y cierre de archivo            *
      * **********************************************************
       9999-FINAL.

           MOVE       WS-CANT-REG-TOTAL        TO WS-CANT-REG-TOT-E

      * Muestra total de registros le•dos                          *
           DISPLAY    WS-TOTAL-TOT
                      WS-CANT-REG-TOT-E
           DISPLAY    "----------------------------------"

      * Muestra total de registros aprobados por corte de control  *
           MOVE       ZEROES                   TO WS-CANT-REG-TOT-E
           MOVE       WS-CANT-REG-TOTAL-OK     TO WS-CANT-REG-TOT-E
           DISPLAY    WS-TOTAL-OK
                      WS-CANT-REG-TOT-E
           DISPLAY    "----------------------------------"

           CLOSE      ENTRADA

           IF         WS-ARCH-ERROR
              DISPLAY '* ERROR EN CLOSE ENTRADA = '
                      WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE
           END-IF

           .

       9999-FINAL-F. EXIT.
