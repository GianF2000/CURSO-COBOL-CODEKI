       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID PGMCORT1.                                             00011000
      **********************************************************        00012000
      *                                                        *        00013000
      *  PROGRAMA PARA EMITIR TOTAL CTAS ACTIVAS POR SUCURSAL  *        00014000
      *  ACTIVAS POR SUC Y  TOTAL CTAS ACTIVAS BCO             *        00015000
      *  LA ENTRADA CLASIFICADA X SUCURSAL                     *        00015100
      *                                                        *        00016000
      **********************************************************        00017000
      *      MANTENIMIENTO DE PROGRAMA                         *        00018000
      **********************************************************        00019000
      *  FECHA   *    DETALLE        * COD *                            00019100
      **************************************                            00019200
      *          *                   *     *                            00019300
      *          *                   *     *                            00019400
      **************************************                            00019500
       ENVIRONMENT DIVISION.                                            00020000
       CONFIGURATION SECTION.                                           00020100
         SPECIAL-NAMES.                                                 00020200
           DECIMAL-POINT IS COMMA.                                      00020300
       INPUT-OUTPUT SECTION.                                            00020400
       FILE-CONTROL.                                                    00020500
             SELECT ENTRADA ASSIGN DDENTRA                              00020600
                    FILE STATUS IS WS-ENT-CODE.                         00020700
                                                                        00020900
       DATA DIVISION.                                                   00039000
       FILE SECTION.                                                    00039100
       FD ENTRADA                                                       00039200
            BLOCK CONTAINS 0 RECORDS                                    00039300
            RECORDING MODE IS F.                                        00039400
                                                                        00039500
       01 REG-ENTRADA    PIC X(20).                                     00039600
                                                                        00039700
      **************************************                            00040400
       WORKING-STORAGE SECTION.                                         00040500
      **************************************                            00040600
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.  00040700
                                                                        00040800
      *****  FILE STATUS  ******************                            00040900
       77  WS-ENT-CODE      PIC XX    VALUE SPACES.                     00041000
       77  WS-SAL-CODE      PIC XX    VALUE SPACES.                     00041100
                                                                        00041200
       01  WS-STATUS-FIN    PIC X.                                      00041300
           88  WS-FIN-LECTURA         VALUE 'Y'.                        00041400
           88  WS-NO-FIN-LECTURA      VALUE 'N'.                        00041500
                                                                        00041600
      *****  COPY DE ENTRADA  ***********************                   00041900
           COPY CORTE.                                                  00042000
                                                                        00046200
      ********  TOTAL CTAS X SUCURSAL ******                            00046300
       77  WS-SUCURSAL-NOM-ANT     PIC X(9)        VALUE "SUCURSAL ".   00046401
       77  WS-SUCURSAL-NUM-ANT     PIC 9(2)        VALUE     ZEROS.     00046701
       77  WS-SUCURSAL-TIP-ANT     PIC 99          VALUE ZEROS.         00046802
       77  WS-SUCURSAL-TIP-ANT-E   PIC Z9          VALUE ZEROS.         00046902
       77  WS-SUCURSAL-IMP-TIP-ANT PIC S9(7)V99   VALUE ZEROS.          00047005
       77  WS-SUCURSAL-IMP-SUC-ANT PIC S9(7)V99 VALUE ZEROS.            00047105
       77  WS-SUCURSAL-IMP-ANT-E   PIC $.$$$.$$9,99 VALUE ZEROS.        00047205
      ********     CONSTANTES       ***************                     00047302
       77  WS-TOTAL PIC X(30) VALUE 'TOTAL GENERAL IMPORTES:      '.    00047405
      ********  TOTAL   ******                                          00047502
       77  WS-CANT-IMP-TOTAL     PIC S9(8)V9(2)  VALUE ZEROS.           00047605
       77  WS-CANT-IMP-TOT-E     PIC $$.$$$.$$9,99   VALUE ZEROS.       00047705
                                                                        00049000
       77  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.  00050800
      ***************************************************************.  00051000
       PROCEDURE DIVISION.                                              00051100
      **************************************                            00052000
      *  CUERPO PRINCIPAL DEL PROGRAMA     *                            00054000
      **************************************                            00057000
       MAIN-PROGRAM.                                                    00060000
           PERFORM 1000-INICIO  THRU   F-1000-INICIO.                   00060200
                                                                        00060300
           PERFORM 2000-PROCESO  THRU  F-2000-PROCESO                   00060400
                   UNTIL WS-FIN-LECTURA.                                00060700
                                                                        00061100
           PERFORM 9999-FINAL    THRU  F-9999-FINAL.                    00061200
                                                                        00061300
       F-MAIN-PROGRAM. GOBACK.                                          00061400
                                                                        00061500
      **************************************                            00061600
      *  CUERPO INICIO APERTURA ARCHIVOS   *                            00061800
      **************************************                            00062000
       1000-INICIO.                                                     00062100
           SET WS-NO-FIN-LECTURA TO TRUE.                               00070200
                                                                        00070300
           OPEN INPUT  ENTRADA.                                         00071000
           IF WS-ENT-CODE IS NOT EQUAL '00'                             00071100
              DISPLAY '* ERROR EN OPEN ENTRADA = ' WS-ENT-CODE          00071200
              MOVE 9999 TO RETURN-CODE                                  00071300
              SET  WS-FIN-LECTURA TO TRUE                               00071400
           END-IF.                                                      00071500
                                                                        00071600
      **************************************                            00073300
      *  PRIMER LECTURA PARA CORTE CONTROL *                            00073400
      **************************************                            00073500
                                                                        00073600
           READ ENTRADA INTO WS-REG-SUCURSAL                            00073700
                        AT END SET WS-FIN-LECTURA TO TRUE.              00073800
                                                                        00073900
           EVALUATE WS-ENT-CODE                                         00074000
              WHEN '00'                                                 00074100
      * Si todo OK, entonces mover NRO e IMPORTE a memoria de respaldo  00074200
                 MOVE WS-SUC-NRO          TO WS-SUCURSAL-NUM-ANT        00074300
                 MOVE WS-SUC-TIPC1        TO WS-SUCURSAL-TIP-ANT        00074401
                 ADD WS-SUC-IMPORTE       TO WS-SUCURSAL-IMP-TIP-ANT    00074502
                                                                        00074601
              WHEN '10'                                                 00074701
                  DISPLAY '* ARCHIVO ENTRADA VAC≤O      ' WS-ENT-CODE   00074801
                  MOVE 9999 TO RETURN-CODE                              00074901
                  SET WS-FIN-LECTURA TO TRUE                            00075001
                                                                        00075101
              WHEN OTHER                                                00075201
                  DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE   00075301
                  MOVE 9999 TO RETURN-CODE                              00075401
                  SET WS-FIN-LECTURA TO TRUE                            00075501
           END-EVALUATE.                                                00075601
GIAN       DISPLAY 'SUCUR / TIPO-CNTA / IMPOR-TIPO / IMPOR-SUCUR'.      00075704
                                                                        00075801
       F-1000-INICIO.   EXIT.                                           00075901
                                                                        00076001
      **************************************                            00076101
      *  CUERPO PRINCIPAL DE PROCESOS      *                            00076200
      *  LECTURA FILE INPUT                *                            00076300
      **************************************                            00076600
       2000-PROCESO.                                                    00076700
                                                                        00076800
           READ ENTRADA INTO WS-REG-SUCURSAL                            00077900
                        AT END SET WS-FIN-LECTURA TO TRUE.              00078000
                                                                        00078100
           EVALUATE WS-ENT-CODE                                         00078200
             WHEN '00'                                                  00078300
              PERFORM 2200-SEGUIR-PROCESO                               00078500
                 THRU F-2200-SEGUIR-PROCESO                             00078600
                                                                        00078700
             WHEN '10'                                                  00078800
              SET WS-FIN-LECTURA TO TRUE                                00078900
              PERFORM 2100-ULTIMO-REGISTRO                              00079000
                 THRU F-2100-ULTIMO-REGISTRO                            00079100
                                                                        00079200
             WHEN OTHER                                                 00079300
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE       00079400
              MOVE 9999 TO RETURN-CODE                                  00079500
              SET WS-FIN-LECTURA TO TRUE                                00079600
                                                                        00079700
           END-EVALUATE.                                                00079800
                                                                        00079900
                                                                        00080000
       F-2000-PROCESO. EXIT.                                            00080100
                                                                        00080200
      **************************************                            00080300
      *  WRITE ULTIMO REGISTRO DE SALIDA   *                            00080500
      **************************************                            00080700
       2100-ULTIMO-REGISTRO.                                            00080800
                                                                        00080900
           PERFORM 2235-CORTE-TIPO-CUENTA THRU F-2235-CORTE-TIPO-CUENTA.00081001
           PERFORM 2230-CORTE-SUCURSAL THRU F-2230-CORTE-SUCURSAL.      00081101
                                                                        00081200
       F-2100-ULTIMO-REGISTRO. EXIT.                                    00083100
                                                                        00083200
      **************************************                            00083300
      *  SEGUIR PROCESO                    *                            00083400
      **************************************                            00083500
       2200-SEGUIR-PROCESO.                                             00083600
                                                                        00083700
           IF WS-SUC-NRO      EQUAL WS-SUCURSAL-NUM-ANT                 00083900
               IF WS-SUC-TIPC1    EQUAL WS-SUCURSAL-TIP-ANT             00084101
      * A∫ado importe al importe total de la sucursal en curso          00084201
                  ADD WS-SUC-IMPORTE       TO WS-SUCURSAL-IMP-TIP-ANT   00084301
               ELSE                                                     00084401
                     PERFORM 2235-CORTE-TIPO-CUENTA THRU                00084501
                           F-2235-CORTE-TIPO-CUENTA                     00084601
               END-IF                                                   00084701
           ELSE                                                         00084801
                 PERFORM 2235-CORTE-TIPO-CUENTA THRU                    00084903
                       F-2235-CORTE-TIPO-CUENTA                         00085003
                 PERFORM 2230-CORTE-SUCURSAL THRU                       00085101
                       F-2230-CORTE-SUCURSAL                            00085201
           END-IF.                                                      00085301
                                                                        00085401
       F-2200-SEGUIR-PROCESO. EXIT.                                     00085501
                                                                        00085601
      **************************************                            00085701
      *  PARRAFO QUE CONTROLA EL CORTE     *                            00085801
      *  DE SUCURSAL                       *                            00085901
      **************************************                            00086001
       2230-CORTE-SUCURSAL.                                             00086101
      ****************************************************              00086200
      *      MUESTRO TOTALES GRABADOS EN SALIDA          *              00086300
      ****************************************************              00086400
           MOVE WS-SUCURSAL-IMP-SUC-ANT TO WS-SUCURSAL-IMP-ANT-E        00086501
           DISPLAY WS-SUCURSAL-NUM-ANT                                  00086604
           " >>>>>>>>>>>>>>>>>>>>>>>>>>> " WS-SUCURSAL-IMP-ANT-E        00086704
           DISPLAY "**************************************************" 00086904
      *                                                                 00087000
      ****************************************************              00088000
      * GUARDO LA SUCURSAL PARA PR‰XIMO CORTE SUCURSAL                  00166600
      ****************************************************              00166700
           MOVE WS-SUC-NRO          TO       WS-SUCURSAL-NUM-ANT        00166900
           ADD  WS-SUCURSAL-IMP-SUC-ANT TO   WS-CANT-IMP-TOTAL          00167101
           MOVE ZEROS               TO       WS-SUCURSAL-IMP-SUC-ANT    00167201
                                             WS-SUCURSAL-IMP-ANT-E      00167301
      * A∫ado importe al importe total de la sucursal en curso          00167400
           .                                                            00167600
       F-2230-CORTE-SUCURSAL. EXIT.                                     00168200
                                                                        00168300
      **************************************                            00168401
      *  PARRAFO QUE CONTROLA EL CORTE     *                            00168501
      *  DE SUCURSAL                       *                            00168601
      **************************************                            00168701
       2235-CORTE-TIPO-CUENTA.                                          00168801
      ****************************************************              00168901
      *      MUESTRO TOTALES GRABADOS EN SALIDA          *              00169001
      ****************************************************              00170001
           MOVE WS-SUCURSAL-IMP-TIP-ANT TO WS-SUCURSAL-IMP-ANT-E        00171001
           MOVE WS-SUCURSAL-TIP-ANT TO WS-SUCURSAL-TIP-ANT-E            00171102
           DISPLAY WS-SUCURSAL-NUM-ANT                                  00171204
                   "        " WS-SUCURSAL-TIP-ANT-E                     00171304
                   "     "  WS-SUCURSAL-IMP-ANT-E                       00171404
           DISPLAY "--------------------------------------------------" 00171504
      *                                                                 00171602
      ****************************************************              00171702
      * GUARDO LA SUCURSAL PARA PR‰XIMO CORTE SUCURSAL                  00171802
      ****************************************************              00171902
           MOVE WS-SUC-TIPC1            TO   WS-SUCURSAL-TIP-ANT        00172002
           ADD  WS-SUCURSAL-IMP-TIP-ANT TO   WS-SUCURSAL-IMP-SUC-ANT    00172102
           MOVE ZEROS                   TO   WS-SUCURSAL-IMP-TIP-ANT    00172202
                                             WS-SUCURSAL-IMP-ANT-E      00172302
                                             WS-SUCURSAL-TIP-ANT-E      00172402
      * A∫ado importe al importe total de la sucursal en curso          00172502
           ADD WS-SUC-IMPORTE       TO       WS-SUCURSAL-IMP-TIP-ANT    00172602
           .                                                            00172702
       F-2235-CORTE-TIPO-CUENTA.  EXIT.                                 00172802
                                                                        00172902
      **************************************                            00173002
      *                                    *                            00173102
      *  CUERPO FINAL CIERRE DE FILES      *                            00173202
      *                                    *                            00173302
      **************************************                            00173402
       9999-FINAL.                                                      00173502
           MOVE WS-CANT-IMP-TOTAL TO WS-CANT-IMP-TOT-E.                 00173602
           DISPLAY WS-TOTAL WS-CANT-IMP-TOT-E.                          00173702
           DISPLAY "----------------------------------".                00173802
                                                                        00173902
           CLOSE ENTRADA                                                00174002
              IF WS-ENT-CODE IS NOT EQUAL '00'                          00174102
                DISPLAY '* ERROR EN CLOSE ENTRADA = '                   00174202
                                            WS-ENT-CODE                 00174302
                MOVE 9999 TO RETURN-CODE                                00174402
                SET WS-FIN-LECTURA TO TRUE                              00174502
             END-IF.                                                    00174602
                                                                        00174702
       F-9999-FINAL.                                                    00175001
           EXIT.                                                        00180000
      *                                                                 00200000
