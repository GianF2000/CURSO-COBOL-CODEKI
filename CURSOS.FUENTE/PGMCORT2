       IDENTIFICATION DIVISION.
       PROGRAM-ID.    PGMCORT2.

      * **********************************************************
      * SINCRONICO 12 v2 - PGMCORT2                              *
      * Realiza doble corte de control de Sucursal y Tip-Cta,    *
      * suma importes por cada Tip-Cta de cada sucursal y        *
      * suma importe total de todo el archivo.                   *
      * v2 de resoluciìn de ejercicio (opcional)                 *
      * **********************************************************
      * JCL: JCLCORT1                                            *
      * ENTRADA: KC03C12.FIX.ASINCR06.PGMCORT1.CORTE.CLAS        *
      * COPY: CORTE                                              *
      * **********************************************************
      * MANTENIMIENTO                                            *
      * 13-04-2025: Creaciìn inicial  * 001                      *
      * 10-07-2025: Reestructuraciìn  * 002                      *
      * **********************************************************

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
        SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
      * ENTRADA es la variable interna del archivo CORTE y se asigna
      * al DDENTRA que es variable externa establecida en el JOB
       FILE-CONTROL.
           SELECT         ENTRADA
           ASSIGN         DDENTRA
           FILE STATUS IS WS-ENT-CODE
           .

      * Establecemos que ENTRADA es de 20 caracteres alfanumericos Fijo
       DATA DIVISION.
       FILE SECTION.
       FD   ENTRADA
            BLOCK CONTAINS 0 RECORDS
            RECORDING MODE IS F
           .
       01  REG-ENTRADA                 PIC X(20)
           .

      * **********************************************************
      * WORKING-STORAGE SECTION: Variables de control y acumuladores
      * **********************************************************
       WORKING-STORAGE SECTION.
      * Cìdigos de estado para archivo ENTRADA:
      * 00 = Operaciìn exitosa
      * 10 = Fin de archivo
      * 01-09, 11-99 = Error en operaciìn
       77  WS-ENT-CODE              PIC X(02)
           .
           88  WS-ARCH-OK                          VALUE '00'
           .
           88  WS-ARCH-FIN-LECTURA                 VALUE '10'
           .
           88  WS-ARCH-ERROR                       VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

      * Estructura de registro de entrada (ver COPY CORTE)
      * LAYOUT: 20 bytes. Cada campo tiene significado y
      * valores posibles:
      *  WS-SUC-NRO   (1:2)   : N£mero de sucursal (99 mximo)
      *  WS-SUC-IMPORTE(3:7)  : Importe (S9(7)V99 COMP-3)
      *  WS-SUC-TIPN  (8:2)   : Tipo novedad (AL=Alta, BA=Baja, DE=D©bito,
      *                         CR=Cr©dito)
      *  WS-SUC-TIPC1 (10:2)  : Tipo cuenta (01=CC, 02=CA, 03=PF)
      *  WS-SUC-TIPC2 (12:1)  : Moneda (1=$, 2=U$S)
      *  FILLER       (13:8)  : Reservado para uso futuro
           COPY CORTE
           .

      * Constantes y mensajes
       77  WS-SUCURSAL-NOM-ANT      PIC X(9)       VALUE 'SUCURSAL '
           .
       77  WS-TOTAL                 PIC X(30)      VALUE
            'TOTAL GENERAL IMPORTES:       '
           .

      * Variables de control de sucursal y tip-cta
       77  WS-SUCURSAL-NUM-ANT      PIC 99         VALUE ZEROS
           .
       77  WS-SUCURSAL-TIP-ANT      PIC 99         VALUE ZEROS
           .
       77  WS-SUCURSAL-TIP-ANT-E    PIC Z9         VALUE ZEROS
           .

      * Acumuladores de importe
       77  WS-SUCURSAL-IMP-TIP-ANT  PIC S9(7)V99   VALUE ZEROS
           .
       77  WS-SUCURSAL-IMP-SUC-ANT  PIC S9(7)V99   VALUE ZEROS
           .
       77  WS-SUCURSAL-IMP-ANT-E    PIC $.$$$.$$9,99
                                                   VALUE ZEROS
           .

      * Total general
       77  WS-CANT-IMP-TOTAL        PIC S9(8)V9(2) VALUE ZEROS
           .
       77  WS-CANT-IMP-TOT-E        PIC $$.$$$.$$9,99
                                                   VALUE ZEROS
           .

      * **********************************************************
      * PROCEDURE DIVISION: Lìgica principal del programa         *
      * **********************************************************
       PROCEDURE DIVISION.

      * Controla el flujo principal del programa: inicializa, procesa y finaliza
      ***************
       MAIN-PROGRAM.
      ***************
           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    WS-ARCH-FIN-LECTURA
                OR    WS-ARCH-ERROR

           PERFORM    9999-FINAL
              THRU    9999-FINAL-F
           .

       MAIN-PROGRAM-F.
           GOBACK.

      ***************
       1000-INICIO.
      ***************
      * Inicializa variables, abre archivo de entrada y realiza primera lectura
           SET        WS-ARCH-OK                   TO TRUE

           OPEN INPUT ENTRADA

           IF NOT     WS-ARCH-OK
              DISPLAY '* ERROR EN OPEN ENTRADA = '
                      WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE
           END-IF

      * Primera lectura para corte de control
           READ       ENTRADA                    INTO WS-REG-SUCURSAL

           EVALUATE   TRUE
             WHEN     WS-ARCH-OK
      * Si todo OK, mover NRO-SUC, TIP-CTA e IMPORTE de primer
      * registro le¥do
              MOVE    WS-SUC-NRO              TO WS-SUCURSAL-NUM-ANT

              MOVE    WS-SUC-TIPC1            TO WS-SUCURSAL-TIP-ANT

              ADD     WS-SUC-IMPORTE          TO WS-SUCURSAL-IMP-TIP-ANT

             WHEN     WS-ARCH-FIN-LECTURA
      * Archivo vac¥o
              DISPLAY '* ARCHIVO ENTRADA VAC²O      ' WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE

             WHEN     WS-ARCH-ERROR
      * Error de lectura
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE
           END-EVALUATE

      * Cabecera del Spool
           DISPLAY    'SUCUR / TIPO-CNTA / IMPOR-TIPO / IMPOR-SUCUR'
           .

       1000-INICIO-F. EXIT.

      ***************
       2000-PROCESO.
      ***************
      * Lee siguiente registro y eval£a cambios de sucursal/tip-cta
           READ       ENTRADA                    INTO WS-REG-SUCURSAL

           EVALUATE   TRUE
             WHEN     WS-ARCH-OK
      * Si lectura OK, comparar valores actuales con anteriores
              PERFORM 2200-SEGUIR-PROCESO
                 THRU 2200-SEGUIR-PROCESO-F

             WHEN     WS-ARCH-FIN-LECTURA
      * Fin de archivo: realizar cortes finales
              PERFORM 2100-ULTIMO-REGISTRO
                 THRU 2100-ULTIMO-REGISTRO-F

             WHEN     WS-ARCH-ERROR
      * Error de lectura
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE
           END-EVALUATE
           .

       2000-PROCESO-F. EXIT.

      **********************
       2100-ULTIMO-REGISTRO.
      **********************
      * Realiza cortes finales de tip-cta y sucursal al terminar archivo
           PERFORM    2235-CORTE-TIPO-CUENTA
              THRU    2235-CORTE-TIPO-CUENTA-F

           PERFORM    2230-CORTE-SUCURSAL
              THRU    2230-CORTE-SUCURSAL-F
           .

       2100-ULTIMO-REGISTRO-F. EXIT.

      **********************
       2200-SEGUIR-PROCESO.
      **********************
      * Eval£a si hubo cambio de sucursal o tip-cta y realiza cortes
      * si corresponde
           IF         WS-SUC-NRO           EQUAL WS-SUCURSAL-NUM-ANT

      * Si no hay cambio de sucursal
               IF     WS-SUC-TIPC1         EQUAL WS-SUCURSAL-TIP-ANT

      * Si no hay cambio de tip-cta, acumula importe
                      ADD WS-SUC-IMPORTE      TO WS-SUCURSAL-IMP-TIP-ANT

               ELSE
      * Cambio de tip-cta: corte de control de tip-cta
                  PERFORM 2235-CORTE-TIPO-CUENTA
                     THRU 2235-CORTE-TIPO-CUENTA-F

               END-IF

           ELSE
      * Cambio de sucursal: corte de tip-cta y luego de sucursal
              PERFORM 2235-CORTE-TIPO-CUENTA
                 THRU 2235-CORTE-TIPO-CUENTA-F

              PERFORM 2230-CORTE-SUCURSAL
                 THRU 2230-CORTE-SUCURSAL-F
           END-IF
           .

       2200-SEGUIR-PROCESO-F. EXIT.

      **********************
       2230-CORTE-SUCURSAL.
      **********************
      * Muestra y resetea acumulado de sucursal, suma al total general
           MOVE       WS-SUCURSAL-IMP-SUC-ANT TO WS-SUCURSAL-IMP-ANT-E

      * Muestra total de importes acumulados por sucursal
           DISPLAY    WS-SUCURSAL-NUM-ANT
                      " >>>>>>>>>>>>>>>>>>>>>>>>>>> "
                      WS-SUCURSAL-IMP-ANT-E

           DISPLAY  "**************************************************"

      * Guarda (copia) valor de nueva sucursal a variable interna
           MOVE       WS-SUC-NRO              TO WS-SUCURSAL-NUM-ANT

      * A‹ado importe acumulado de sucursal al importe total del arch
           ADD        WS-SUCURSAL-IMP-SUC-ANT TO WS-CANT-IMP-TOTAL

      * Resetea el valor de los importes de sucursal a zero
           MOVE       ZEROS                   TO WS-SUCURSAL-IMP-SUC-ANT
                                                 WS-SUCURSAL-IMP-ANT-E
           .

       2230-CORTE-SUCURSAL-F. EXIT.

      *************************
       2235-CORTE-TIPO-CUENTA.
      *************************
      * Muestra y resetea acumulado de tip-cta, suma a sucursal
           MOVE       WS-SUCURSAL-IMP-TIP-ANT   TO WS-SUCURSAL-IMP-ANT-E

           MOVE       WS-SUCURSAL-TIP-ANT       TO WS-SUCURSAL-TIP-ANT-E

      * Muestra total de importes acumulados de TIP-DOC asociado a SUCUR
           DISPLAY    WS-SUCURSAL-NUM-ANT
                      "        "
                      WS-SUCURSAL-TIP-ANT-E
                      "     "
                      WS-SUCURSAL-IMP-ANT-E
           DISPLAY  "--------------------------------------------------"

      * Guarda (copia) valor de nueva TIP-DOC a variable interna
           MOVE       WS-SUC-TIPC1            TO WS-SUCURSAL-TIP-ANT

      * A‹ado importe acumulado de TIP-DOC al importe total del
      * sucursal asociado.
           ADD        WS-SUCURSAL-IMP-TIP-ANT TO WS-SUCURSAL-IMP-SUC-ANT

      * Resetea el valor de los importes de TIP-DOC a zero
           MOVE       ZEROS                   TO WS-SUCURSAL-IMP-TIP-ANT
                                                 WS-SUCURSAL-IMP-ANT-E
                                                 WS-SUCURSAL-TIP-ANT-E

      * Muevo impor de sucursal actual a impor acumulado de nuev tip-doc
           ADD        WS-SUC-IMPORTE          TO WS-SUCURSAL-IMP-TIP-ANT
           .

       2235-CORTE-TIPO-CUENTA-F.  EXIT.

      ***************
       9999-FINAL.
      ***************
      * Muestra total general, cierra archivo y controla errores de cierre
           MOVE       WS-CANT-IMP-TOTAL            TO WS-CANT-IMP-TOT-E

           DISPLAY    WS-TOTAL
                      WS-CANT-IMP-TOT-E

           DISPLAY    "----------------------------------------"

           CLOSE      ENTRADA

           IF         WS-ARCH-ERROR
               DISPLAY '* ERROR EN CLOSE ENTRADA = '
                       WS-ENT-CODE

               MOVE    9999                         TO RETURN-CODE
           END-IF
           .

       9999-FINAL-F. EXIT.
