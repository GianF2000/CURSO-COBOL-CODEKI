       IDENTIFICATION DIVISION.
      *                                                        *
       PROGRAM-ID PGMAPA02.
      **********************************************************
      *                                                        *
      *                      SINCRONICO 16                     *
      *                                                        *
      **********************************************************
      *  Realiza el apareo de un arch maestro de clie con un   *
      *  arch de movi, actualizando los saldos de los clie y   *
      *  generando un arch de salida con los clie actualizados.*
      *****************************************************************
      *   JCL    *                  ARCHIVOS                          *
      *****************************************************************
      * COMPILA  * FILE1: KC03C12.FIX.SINCR16.PGMAPA02.E1.CLIENTE     *
      * JCLAPAR  * FILE2: KC03C12.FIX.SINCR16.PGMAPA02.E2.MOVIMCC     *
      *          * FILE3: KC03C12.FIX.SINCR16.PGMAPA02.S1.CLIENTE     *
      *                                                               *
      *****************************************************************
      ****************************************
      *      MANTENIMIENTO DE PROGRAMA       *
      ****************************************
      *  FECHA     *    DETALLE        * COD *
      ****************************************
      * 15-04-2025 * Creacion          * 001 *
      *            *                   *     *
      ****************************************

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
      * FILE1 es el archivo maestro de clientes (entrada)
      * FILE2 es el archivo de movimientos (entrada)
      * FILE3 es el archivo de salida (clientes actualizados)
       FILE-CONTROL.
           SELECT         FILE1
           ASSIGN         DDFILE1
           FILE STATUS IS FS-FILE1
           .
           SELECT         FILE2
           ASSIGN         DDFILE2
           FILE STATUS IS FS-FILE2
           .
           SELECT         FILE3
           ASSIGN         DDFILE3
           FILE STATUS IS FS-FILE3
           .

       DATA DIVISION.
       FILE SECTION.
       FD  FILE1
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.
       01  REG-FILE1                  PIC X(30)
           .

       FD  FILE2
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.
       01  REG-FILE2                  PIC X(80)
           .

       FD  FILE3
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F.
       01  REG-FILE3                  PIC X(30)
           .

      **************************************
       WORKING-STORAGE SECTION.
      **************************************
       77  FILLER                   PIC X(26)      VALUE
            '* INICIO WORKING-STORAGE *'
           .

      *****  FILE STATUS  ******************
       77  FS-FILE1                 PIC X(02)
           .
           88  FS1-OK                              VALUE '00'
           .
           88  FS1-FIN-LECTURA                     VALUE '10'
           .
           88  FS1-ERROR                           VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

       77  FS-FILE2                 PIC X(02)
           .
           88  FS2-OK                              VALUE '00'
           .
           88  FS2-FIN-LECTURA                     VALUE '10'
           .
           88  FS2-ERROR                           VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

       77  FS-FILE3                 PIC X(02)
           .
           88  FS3-OK                              VALUE '00'
           .
           88  FS3-ERROR                           VALUE '01' THRU '09'
                                                         '10' THRU '99'
           .

      *****  COPIAS DE ESTRUCTURAS DE ENTRADA Y MOVIMIENTOS *****
           COPY CLIENTE
           .
           COPY MOVIMCC
           .

      *****  CLAVES DE CONTROL PARA APAREO ******
       01  WS-CLAVE1
           .
           03  WS-TIP-DOC1           PIC X(02)     VALUE ZEROS
           .
           03  WS-NRO-DOC1           PIC 9(08)     VALUE ZEROS
           .

       01  WS-CLAVE2
           .
           03  WS-TIP-DOC2           PIC X(02)     VALUE ZEROS
           .
           03  WS-NRO-DOC2           PIC 9(08)     VALUE ZEROS
           .

      *****  SALDO TEMPORAL  ********************
       01  WS-FIL-SALDO              PIC S9(09)V99 VALUE ZEROS
           .

      *****  CONTADORES DE CONTROL  *************
       77  WS-LEIDOS-FILE1           PIC 9(05)    VALUE ZEROS
           .
       77  WS-LEIDOS-FILE2           PIC 9(05)    VALUE ZEROS
           .
       77  WS-ENCONTRADOS            PIC 9(05)    VALUE ZEROS
           .
       77  WS-NO-ENCONTRADO          PIC 9(05)    VALUE ZEROS
           .

      *****  LEYENDAS PARA DISPLAY DE TOTALES ***
       77  WS-LEYEN-FILE1            PIC X(35)    VALUE
            'CANTIDAD DE LEIDOS CLIENTES  :   '
           .
       77  WS-LEYEN-FILE2            PIC X(35)    VALUE
            'CANTIDAD DE LEIDOS MOVIMIENT :   '
           .
       77  WS-LEYEN-ENCONTRADOS      PIC X(35)    VALUE
            'CANTIDAD ENCONTRADOS          :  '
           .
       77  WS-LEYEN-NO-ENCONTRADO    PIC X(35)    VALUE
            'CANTIDAD DE NO ENCONTRADOS    :  '
           .

      *****  FECHA DE PROCESO  ******************
       01  WS-FECHA
           .
           03  WS-FECHA-AA           PIC 99       VALUE ZEROS
           .
           03  WS-FECHA-MM           PIC 99       VALUE ZEROS
           .
           03  WS-FECHA-DD           PIC 99       VALUE ZEROS
           .

      *****  BANDERA DE FIN DE PROCESO *******
       01  WS-STATUS-FIN             PIC X
           .
           88  WS-FIN-LECTURA                      VALUE 'Y'
           .
           88  WS-NO-FIN-LECTURA                   VALUE 'N'
           .

       77  FILLER                   PIC X(26)      VALUE
            '* FINAL  WORKING-STORAGE *'
           .

      ***************************************************************.
       PROCEDURE DIVISION.
      ***************
       MAIN-PROGRAM.
      ***************
      * Programa principal: controla el flujo general del proceso
           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    WS-FIN-LECTURA
                OR    FS1-ERROR
                OR    FS2-ERROR

           PERFORM    3000-FINAL
              THRU    3000-FINAL-F

           .

       MAIN-PROGRAM-F. GOBACK.

      ***************
       1000-INICIO.
      ***************
      * Apertura de archivos y preparaciìn de variables
           ACCEPT     WS-FECHA                   FROM DATE

      * Inicializa bandera de fin de proceso
           SET WS-NO-FIN-LECTURA TO TRUE

      * Abrir archivo maestro de clientes
           OPEN INPUT FILE1
           IF         FS1-ERROR
              DISPLAY '* ERROR EN OPEN FILE1   = '
                      FS-FILE1
              MOVE    9999                         TO RETURN-CODE
           END-IF

      * Abrir archivo de movimientos
           OPEN INPUT FILE2
           IF         FS2-ERROR
              DISPLAY '* ERROR EN OPEN MOVIMI  = '
                      FS-FILE2
              MOVE    9999                         TO RETURN-CODE
           END-IF

      * Abrir archivo de salida
           OPEN OUTPUT FILE3
           IF         FS3-ERROR
              DISPLAY '* ERROR EN OPEN SALIDA  = '
                      FS-FILE3
              MOVE    9999                         TO RETURN-CODE
           END-IF

      * Leer primer registro de cada archivo de entrada
           PERFORM    1100-LEER-FILE1
              THRU    1100-LEER-FILE1-F
           PERFORM    1200-LEER-FILE2
              THRU    1200-LEER-FILE2-F
           .

       1000-INICIO-F. EXIT.

      ***************
       2000-PROCESO.
      ***************
      * Apareo de archivos: compara claves y actualiza saldos
           IF         WS-CLAVE1
           =          WS-CLAVE2
      * Si las claves coinciden, sumar el importe al saldo temporal
              ADD     1                            TO WS-ENCONTRADOS
              ADD     WS-MOV-IMPORTE               TO WS-FIL-SALDO
              DISPLAY 'REG. ACTUALIZADO - '
                      WS-CLAVE2
      * Leer siguiente movimiento
              PERFORM 1200-LEER-FILE2
                 THRU 1200-LEER-FILE2-F

      * Si las claves no coinciden, verificar cul es mayor
           ELSE
              IF      WS-CLAVE1
              >       WS-CLAVE2
      * Si la clave del maestro es mayor, el movimiento no tiene cliente
                 ADD     1                         TO WS-NO-ENCONTRADO
                 DISPLAY 'NO HAY MAESTRO PARA ESTE MOV: '
                         WS-CLAVE2
      * Leer siguiente movimiento
                 PERFORM 1200-LEER-FILE2
                    THRU 1200-LEER-FILE2-F

              ELSE
      * Si la clave del maestro es menor, grabar cliente con saldo
                 ADD     WS-FIL-SALDO              TO WS-CLI-SALDO
                 WRITE   REG-FILE3               FROM WS-REG-CLIENTE
                 MOVE    0                         TO WS-FIL-SALDO
      * Leer siguiente cliente
                 PERFORM 1100-LEER-FILE1
                    THRU 1100-LEER-FILE1-F
              END-IF
           END-IF

      * Si ambos archivos llegaron a fin, terminar proceso
           IF         FS1-FIN-LECTURA
           AND        FS2-FIN-LECTURA
              SET     WS-FIN-LECTURA               TO TRUE
           END-IF
           .

       2000-PROCESO-F. EXIT.

      ***************
       1100-LEER-FILE1.
      ***************
      * Lee un registro del archivo maestro de clientes
           READ       FILE1                      INTO WS-REG-CLIENTE

           EVALUATE   TRUE
           WHEN       FS1-OK
      * Si la lectura fue exitosa, actualizar clave y contador
              ADD     1                            TO WS-LEIDOS-FILE1
              MOVE    WS-CLI-TIPO                  TO WS-TIP-DOC1
              MOVE    WS-CLI-CUENTA                TO WS-NRO-DOC1

           WHEN       FS1-FIN-LECTURA
      * Si llegì al final del archivo, poner clave en HIGH-VALUE
              MOVE    HIGH-VALUE                    TO WS-CLAVE1

           WHEN       OTHER
      * Si hubo error, mostrar mensaje
              DISPLAY '* ERROR EN LECTURA FILE1   = '
                      FS-FILE1
              MOVE    9999                          TO RETURN-CODE
           END-EVALUATE
           .

       1100-LEER-FILE1-F. EXIT.

      ***************
       1200-LEER-FILE2.
      ***************
      * Lee un registro del archivo de movimientos
           READ       FILE2                       INTO WS-REG-MOVIMI

           EVALUATE   TRUE
           WHEN       FS2-OK
      * Si la lectura fue exitosa, actualizar clave y contador
              ADD     1                             TO WS-LEIDOS-FILE2
              MOVE    WS-MOV-TIPO                   TO WS-TIP-DOC2
              MOVE    WS-MOV-CUENTA                 TO WS-NRO-DOC2

           WHEN       FS2-FIN-LECTURA
      * Si llegì al final del archivo, poner clave en HIGH-VALUE
              MOVE    HIGH-VALUE                    TO WS-CLAVE2

           WHEN       OTHER
      * Si hubo error, mostrar mensaje
              DISPLAY '* ERROR EN LECTURA FILE2   = '
                      FS-FILE2
              MOVE    9999                          TO RETURN-CODE
           END-EVALUATE
           .

       1200-LEER-FILE2-F. EXIT.

      ***************
       3000-FINAL.
      ***************
      * Cierre de archivos y muestra de totales
           IF         RETURN-CODE                  NOT EQUAL 9999
              PERFORM 3010-CLOSE-FILES
                 THRU 3010-CLOSE-FILES-F
              PERFORM 3020-MOSTRAR-TOTALES
                 THRU 3020-MOSTRAR-TOTALES-F
           END-IF
           .

       3000-FINAL-F. EXIT.

      ********************
       3010-CLOSE-FILES.
      ********************
      * Cierra todos los archivos y muestra errores si ocurren
           CLOSE      FILE1
           IF         FS1-ERROR
              DISPLAY '* ERROR EN CLOSE FILE1   = '
                      FS-FILE1
              MOVE    9999                          TO RETURN-CODE
           END-IF

           CLOSE      FILE2
           IF         FS2-ERROR
              DISPLAY '* ERROR EN CLOSE FILE2   = '
                      FS-FILE2
              MOVE    9999                          TO RETURN-CODE
           END-IF

           CLOSE      FILE3
           IF         FS3-ERROR
              DISPLAY '* ERROR EN CLOSE FILE3   = '
                      FS-FILE3
              MOVE    9999                          TO RETURN-CODE
           END-IF
           .

       3010-CLOSE-FILES-F. EXIT.

      ************************
       3020-MOSTRAR-TOTALES.
      ************************
      * Muestra los totales de registros procesados
           DISPLAY    WS-LEYEN-FILE1
                      WS-LEIDOS-FILE1

           DISPLAY    WS-LEYEN-FILE2
                      WS-LEIDOS-FILE2

           DISPLAY    WS-LEYEN-ENCONTRADOS
                      WS-ENCONTRADOS

           DISPLAY    WS-LEYEN-NO-ENCONTRADO
                      WS-NO-ENCONTRADO
           .

       3020-MOSTRAR-TOTALES-F. EXIT.
