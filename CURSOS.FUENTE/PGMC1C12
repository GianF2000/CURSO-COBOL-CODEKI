       IDENTIFICATION DIVISION.
       PROGRAM-ID. PGMC1C12.

      ****************************************************************
      *  PROGRAMA   : PGMC1C12                                       *
      *  AUTOR      : (Nombre del autor)                             *
      *  FECHA      : 14-04-2025                                     *
      *  DESCRIPCION: S¥ncrono 13.2 - Cuenta y muestra la cantidad   *
      *               de registros por estado civil (SOLTERO,        *
      *               CASADO, VIUDO, DIVORCIADO) le¥dos desde el     *
      *               archivo CLICOB, mostrando tambi©n el total de  *
      *               registros le¥dos.                              *
      ****************************************************************
      *  JCL EJEC   : JCLC1C12                                       *
      *  ENTRADA    : KC03C12.FIX.SINCR132.PGMC1C12.CLICOB           *
      *               (entrada, 93 bytes, formato fijo)              *
      *  COPYS      : CLICOB (estructura de cliente, ver detalles    *
      *               t©cnicos abajo)                                *
      ****************************************************************
      *  MANTENIMIENTO                                              *
      *  FECHA     *  DETALLE              *  COD                   *
      * 14-04-2025 *  Creaciìn             *  001                   *
      * 10-07-2025 *  Reestructuraciìn     *  002                   *
      *            *  profesional          *                        *
      ****************************************************************
      *  DETALLES TECNICOS DE LA COPY CLICOB                       *
      *  - Estructura: contiene datos de cliente, incluyendo        *
      *    estado civil en WS-SUC-EST-CIV (X(10)), valores          *
      *    posibles: 'SOLTERO   ', 'CASADO    ', 'VIUDO     ',      *
      *    'DIVORCIADO'.                                           *
      *  - Otros campos: ver COPY CLICOB para detalles de layout    *
      *    y uso.                                                  *
      ****************************************************************

      ****************************************************************
      * ENVIRONMENT DIVISION                                         *
      ****************************************************************
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
      * Archivo de entrada: E1CLIEN (CLICOB), asignado a DDCLIEN     *
      * en el JCL. File status controlado por FS-E1CLIEN.            *
       FILE-CONTROL.
           SELECT         E1CLIEN
           ASSIGN         DDCLIEN
           FILE STATUS IS FS-E1CLIEN
           .

      ****************************************************************
      * DATA DIVISION                                                *
      ****************************************************************
       DATA DIVISION.
       FILE SECTION.
      * E1CLIEN: archivo de 93 caracteres, formato fijo.             *
       FD  E1CLIEN
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           .

       01  REG-E1CLIEN               PIC X(93)
           .

      ****************************************************************
      * WORKING-STORAGE SECTION                                      *
      ****************************************************************
       WORKING-STORAGE SECTION.

      * File status para E1CLIEN                                     *
       77  FS-E1CLIEN               PIC X(02)
           .
           88  WS-ARCH-OK                          VALUE '00'
           .
           88  WS-ARCH-FIN-LECTURA                 VALUE '10'
           .
           88  WS-ARCH-ERROR                       VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

      * COPY CLICOB: incluye WS-SUC-EST-CIV (estado civil)           *
           COPY CLICOB
           .

      * Constantes de estado civil                                   *
       77  WS-CLI-SO                PIC X(10)      VALUE 'SOLTERO   '
           .
       77  WS-CLI-CA                PIC X(10)      VALUE 'CASADO    '
           .
       77  WS-CLI-VI                PIC X(10)      VALUE 'VIUDO     '
           .
       77  WS-CLI-DI                PIC X(10)      VALUE 'DIVORCIADO'
           .

      * Contadores de totales y por estado civil                     *
       77  WS-CANT-LEIDOS           PIC 9(3)       VALUE ZEROS
           .
       77  WS-CANT-SO               PIC 9(3)       VALUE ZEROS
           .
       77  WS-CANT-CA               PIC 9(3)       VALUE ZEROS
           .
       77  WS-CANT-VI               PIC 9(3)       VALUE ZEROS
           .
       77  WS-CANT-DI               PIC 9(3)       VALUE ZEROS
           .

      ****************************************************************
      * PROCEDURE DIVISION: Lìgica principal del programa         *
      ****************************************************************
       PROCEDURE DIVISION.

      * **********************************************************
      * MAIN-PROGRAM: Control de ejecuciìn principal              *
      * **********************************************************
       MAIN-PROGRAM.

           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    WS-ARCH-FIN-LECTURA
                OR    WS-ARCH-ERROR

           PERFORM    3000-FINAL
              THRU    3000-FINAL-F
           .

       MAIN-PROGRAM-F. GOBACK.

      * **********************************************************
      * 1000-INICIO: Inicializaciìn y apertura de archivo         *
      * **********************************************************
       1000-INICIO.

           SET        WS-ARCH-OK                   TO TRUE

           OPEN INPUT E1CLIEN

           IF NOT     WS-ARCH-OK
              DISPLAY '* ERROR EN OPEN E1CLIEN = '
                      FS-E1CLIEN
              MOVE    9999                         TO RETURN-CODE

      * Primera lectura
           ELSE
              PERFORM 2100-LEER
                 THRU 2100-LEER-F

      * Si el archivo estuviera vac¥o, notificarlo                *
              IF      WS-ARCH-FIN-LECTURA
                 DISPLAY '* ARCHIVO ENTRADA VAC²O '
                         FS-E1CLIEN
                 MOVE    9999                      TO RETURN-CODE
              END-IF
           END-IF
           .

       1000-INICIO-F. EXIT.

      * **********************************************************
      * 2000-PROCESO: Lectura y conteo por estado civil           *
      * **********************************************************
       2000-PROCESO.

      * Evaluamos Estado Civil y aumentamos en 1 al contador respectivo.
           EVALUATE   WS-SUC-EST-CIV

           WHEN       WS-CLI-SO
              ADD     1                            TO WS-CANT-SO

           WHEN       WS-CLI-CA
              ADD     1                            TO WS-CANT-CA

           WHEN       WS-CLI-VI
              ADD     1                            TO WS-CANT-VI

           WHEN       WS-CLI-DI
              ADD     1                            TO WS-CANT-DI

           WHEN OTHER
              DISPLAY "ESTADO CIVIL NO VALIDO - "
                      WS-SUC-EST-CIV

           END-EVALUATE

           PERFORM    2100-LEER
              THRU    2100-LEER-F
           .

       2000-PROCESO-F. EXIT.

      * **********************************************************
      * 2100-LEER: Lectura de registro y control de file status   *
      * **********************************************************
       2100-LEER.
      ***************
           READ       E1CLIEN                    INTO WS-REG-CLICOB

           EVALUATE   TRUE

               WHEN   WS-ARCH-OK
                  ADD     1                        TO WS-CANT-LEIDOS

               WHEN   WS-ARCH-FIN-LECTURA
                  CONTINUE

               WHEN   WS-ARCH-ERROR
                  DISPLAY '* ERROR EN LECTURA SUCURSAL= '
                          FS-E1CLIEN

                  MOVE    9999                     TO RETURN-CODE

           END-EVALUATE
           .

       2100-LEER-F. EXIT.

      * **********************************************************
      * 3000-FINAL: Cierre de archivo y mostrar totales           *
      * **********************************************************
       3000-FINAL.

      * Si no hay errores, cierra archivo y muestra totales       *
           IF          RETURN-CODE                NOT EQUAL 9999
              PERFORM  3010-CLOSE-FILES
                 THRU  3010-CLOSE-FILES-F

              PERFORM  3020-MOSTRAR-TOTALES
                 THRU  3020-MOSTRAR-TOTALES-F

           END-IF

           .

       3000-FINAL-F. EXIT.

      * **********************************************************
      * 3010-CLOSE-FILES: Cierre de archivos                      *
      * **********************************************************
       3010-CLOSE-FILES.

           CLOSE       E1CLIEN

           IF          WS-ARCH-ERROR
              DISPLAY  '* ERROR EN CLOSE SUCURSAL= '
                       FS-E1CLIEN

              MOVE     9999                        TO RETURN-CODE
           END-IF
           .

       3010-CLOSE-FILES-F. EXIT.

      * **********************************************************
      * 3020-MOSTRAR-TOTALES: Mostrar totales en pantalla         *
      * **********************************************************
       3020-MOSTRAR-TOTALES.
           DISPLAY     ' TOTAL DE E1CLIENS LEIDAS '
                       WS-CANT-LEIDOS

           DISPLAY     ' TOTAL DE SOLTEROS        '
                       WS-CANT-SO

           DISPLAY     ' TOTAL DE CASADOS         '
                       WS-CANT-CA

           DISPLAY     ' TOTAL DE VIUDOS          '
                       WS-CANT-VI

           DISPLAY     ' TOTAL DE DIVORCIADOS     '
                       WS-CANT-DI
           .

       3020-MOSTRAR-TOTALES-F. EXIT.

