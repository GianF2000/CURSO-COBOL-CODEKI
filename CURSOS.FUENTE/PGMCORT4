       IDENTIFICATION DIVISION.
       PROGRAM-ID PGMCORT1.
      **********************************************************
      *                                                        *
      *  PROGRAMA PARA EMITIR TOTAL CTAS ACTIVAS POR SUCURSAL  *
      *  ACTIVAS POR SUC Y  TOTAL CTAS ACTIVAS BCO             *
      *  LA ENTRADA CLASIFICADA X SUCURSAL                     *
      *                                                        *
      **********************************************************
      *      MANTENIMIENTO DE PROGRAMA                         *
      **********************************************************
      *  FECHA   *    DETALLE        * COD *
      **************************************
      *          *                   *     *
      *          *                   *     *
      **************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
         SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
             SELECT ENTRADA ASSIGN DDENTRA
                    FILE STATUS IS WS-ENT-CODE.

       DATA DIVISION.
       FILE SECTION.
       FD ENTRADA
            BLOCK CONTAINS 0 RECORDS
            RECORDING MODE IS F.

       01 REG-ENTRADA    PIC X(20).

      **************************************
       WORKING-STORAGE SECTION.
      **************************************
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.

      *****  FILE STATUS  ******************
       77  WS-ENT-CODE      PIC XX    VALUE SPACES.
       77  WS-SAL-CODE      PIC XX    VALUE SPACES.

       01  WS-STATUS-FIN    PIC X.
           88  WS-FIN-LECTURA         VALUE 'Y'.
           88  WS-NO-FIN-LECTURA      VALUE 'N'.

      *****  COPY DE ENTRADA  ***********************
           COPY CORTE.

      ********  TOTAL CTAS X SUCURSAL ******
       77  WS-SUCURSAL-NOM-ANT     PIC X(9)        VALUE "SUCURSAL ".
       77  WS-SUCURSAL-NUM-ANT     PIC 9(2)        VALUE     ZEROS.
       77  WS-SUCURSAL-TIP-ANT     PIC 99          VALUE ZEROS.
       77  WS-SUCURSAL-TIP-ANT-E   PIC Z9          VALUE ZEROS.
       77  WS-SUCURSAL-IMP-TIP-ANT PIC S9(7)V99   VALUE ZEROS.
       77  WS-SUCURSAL-IMP-SUC-ANT PIC S9(7)V99 VALUE ZEROS.
       77  WS-SUCURSAL-IMP-ANT-E   PIC $$.$$$.$$9,99 VALUE ZEROS.
      ********     CONSTANTES       ***************
       77  WS-TOTAL PIC X(24) VALUE 'TOTAL GENERAL IMPORTES: '.
      ********  TOTAL   ******
       77  WS-CANT-IMP-TOTAL     PIC S9(8)V9(2)  VALUE ZEROS.
       77  WS-CANT-IMP-TOT-E     PIC $$.$$$.$$9,99   VALUE ZEROS.

       77  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.
      ***************************************************************.
       PROCEDURE DIVISION.
      **************************************
      *  CUERPO PRINCIPAL DEL PROGRAMA     *
      **************************************
       MAIN-PROGRAM.
           PERFORM 1000-INICIO  THRU   F-1000-INICIO.

           PERFORM 2000-PROCESO  THRU  F-2000-PROCESO
                   UNTIL WS-FIN-LECTURA.

           PERFORM 9999-FINAL    THRU  F-9999-FINAL.

       F-MAIN-PROGRAM. GOBACK.

      **************************************
      *  CUERPO INICIO APERTURA ARCHIVOS   *
      **************************************
       1000-INICIO.
           SET WS-NO-FIN-LECTURA TO TRUE.

           OPEN INPUT  ENTRADA.
           IF WS-ENT-CODE IS NOT EQUAL '00'
              DISPLAY '* ERROR EN OPEN ENTRADA = ' WS-ENT-CODE
              MOVE 9999 TO RETURN-CODE
              SET  WS-FIN-LECTURA TO TRUE
           END-IF.

      **************************************
      *  PRIMER LECTURA PARA CORTE CONTROL *
      **************************************

           READ ENTRADA INTO WS-REG-SUCURSAL
                        AT END SET WS-FIN-LECTURA TO TRUE.

           EVALUATE WS-ENT-CODE
              WHEN '00'
                 IF WS-SUC-TIPC2 = 1
      * Si todo OK, entonces mover NRO e IMPORTE a memoria de respaldo
                     MOVE WS-SUC-NRO          TO WS-SUCURSAL-NUM-ANT
                     MOVE WS-SUC-TIPC1        TO WS-SUCURSAL-TIP-ANT
                     ADD WS-SUC-IMPORTE       TO WS-SUCURSAL-IMP-TIP-ANT
                     DISPLAY WS-SUCURSAL-NOM-ANT  WS-SUCURSAL-NUM-ANT
                 END-IF

              WHEN '10'
                  DISPLAY '* ARCHIVO ENTRADA VAC≤O      ' WS-ENT-CODE
                  MOVE 9999 TO RETURN-CODE
                  SET WS-FIN-LECTURA TO TRUE

              WHEN OTHER
                  DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE
                  MOVE 9999 TO RETURN-CODE
                  SET WS-FIN-LECTURA TO TRUE
           END-EVALUATE.

       F-1000-INICIO.   EXIT.

      **************************************
      *  CUERPO PRINCIPAL DE PROCESOS      *
      *  LECTURA FILE INPUT                *
      **************************************
       2000-PROCESO.

           READ ENTRADA INTO WS-REG-SUCURSAL
                        AT END SET WS-FIN-LECTURA TO TRUE.

           EVALUATE WS-ENT-CODE
             WHEN '00'
                 IF WS-SUC-TIPC2 = 1
                      PERFORM 2200-SEGUIR-PROCESO
                         THRU F-2200-SEGUIR-PROCESO
                 END-IF
             WHEN '10'
              SET WS-FIN-LECTURA TO TRUE
              PERFORM 2100-ULTIMO-REGISTRO
                 THRU F-2100-ULTIMO-REGISTRO

             WHEN OTHER
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE
              MOVE 9999 TO RETURN-CODE
              SET WS-FIN-LECTURA TO TRUE

           END-EVALUATE.


       F-2000-PROCESO. EXIT.

      **************************************
      *  WRITE ULTIMO REGISTRO DE SALIDA   *
      **************************************
       2100-ULTIMO-REGISTRO.

           PERFORM 2235-CORTE-TIPO-CUENTA THRU F-2235-CORTE-TIPO-CUENTA.
           PERFORM 2230-CORTE-SUCURSAL THRU F-2230-CORTE-SUCURSAL.

       F-2100-ULTIMO-REGISTRO. EXIT.

      **************************************
      *  SEGUIR PROCESO                    *
      **************************************
       2200-SEGUIR-PROCESO.

           IF WS-SUC-NRO      EQUAL WS-SUCURSAL-NUM-ANT
               IF WS-SUC-TIPC1    EQUAL WS-SUCURSAL-TIP-ANT
      * A∫ado importe al importe total de la sucursal en curso
                  ADD WS-SUC-IMPORTE       TO WS-SUCURSAL-IMP-TIP-ANT
               ELSE
                     PERFORM 2235-CORTE-TIPO-CUENTA THRU
                           F-2235-CORTE-TIPO-CUENTA
               END-IF
           ELSE
                 PERFORM 2235-CORTE-TIPO-CUENTA THRU
                       F-2235-CORTE-TIPO-CUENTA
                 PERFORM 2230-CORTE-SUCURSAL THRU
                       F-2230-CORTE-SUCURSAL
                 DISPLAY WS-SUCURSAL-NOM-ANT       WS-SUCURSAL-NUM-ANT
           END-IF.

       F-2200-SEGUIR-PROCESO. EXIT.

      **************************************
      *  PARRAFO QUE CONTROLA EL CORTE     *
      *  DE SUCURSAL                       *
      **************************************
       2230-CORTE-SUCURSAL.
      ****************************************************
      *      MUESTRO TOTALES GRABADOS EN SALIDA          *
      ****************************************************
           MOVE WS-SUCURSAL-IMP-SUC-ANT TO WS-SUCURSAL-IMP-ANT-E
      *    DISPLAY WS-SUCURSAL-NOM-ANT       WS-SUCURSAL-NUM-ANT
           DISPLAY "                        "  WS-SUCURSAL-IMP-ANT-E
           DISPLAY "**********************************"
      *
      ****************************************************
      * GUARDO LA SUCURSAL PARA PR‰XIMO CORTE SUCURSAL
      ****************************************************
           MOVE WS-SUC-NRO          TO       WS-SUCURSAL-NUM-ANT
           ADD  WS-SUCURSAL-IMP-SUC-ANT TO   WS-CANT-IMP-TOTAL
           MOVE ZEROS               TO       WS-SUCURSAL-IMP-SUC-ANT
                                             WS-SUCURSAL-IMP-ANT-E
      * A∫ado importe al importe total de la sucursal en curso
           .
       F-2230-CORTE-SUCURSAL. EXIT.

      **************************************
      *  PARRAFO QUE CONTROLA EL CORTE     *
      *  DE SUCURSAL                       *
      **************************************
       2235-CORTE-TIPO-CUENTA.
      ****************************************************
      *      MUESTRO TOTALES GRABADOS EN SALIDA          *
      ****************************************************
           MOVE WS-SUCURSAL-IMP-TIP-ANT TO WS-SUCURSAL-IMP-ANT-E
           MOVE WS-SUCURSAL-TIP-ANT TO WS-SUCURSAL-TIP-ANT-E
      *    DISPLAY WS-SUCURSAL-NOM-ANT       WS-SUCURSAL-NUM-ANT
           DISPLAY "     Tipo de cuenta "    WS-SUCURSAL-TIP-ANT-E
                   "  "  WS-SUCURSAL-IMP-ANT-E
      *    DISPLAY "**********************************"
      *
      ****************************************************
      * GUARDO LA SUCURSAL PARA PR‰XIMO CORTE SUCURSAL
      ****************************************************
           MOVE WS-SUC-TIPC1            TO   WS-SUCURSAL-TIP-ANT
           ADD  WS-SUCURSAL-IMP-TIP-ANT TO   WS-SUCURSAL-IMP-SUC-ANT
           MOVE ZEROS                   TO   WS-SUCURSAL-IMP-TIP-ANT
                                             WS-SUCURSAL-IMP-ANT-E
                                             WS-SUCURSAL-TIP-ANT-E
      * A∫ado importe al importe total de la sucursal en curso
           ADD WS-SUC-IMPORTE       TO       WS-SUCURSAL-IMP-TIP-ANT
           .
       F-2235-CORTE-TIPO-CUENTA.  EXIT.

      **************************************
      *                                    *
      *  CUERPO FINAL CIERRE DE FILES      *
      *                                    *
      **************************************
       9999-FINAL.
           MOVE WS-CANT-IMP-TOTAL TO WS-CANT-IMP-TOT-E.
           DISPLAY WS-TOTAL WS-CANT-IMP-TOT-E.
           DISPLAY "----------------------------------".

           CLOSE ENTRADA
              IF WS-ENT-CODE IS NOT EQUAL '00'
                DISPLAY '* ERROR EN CLOSE ENTRADA = '
                                            WS-ENT-CODE
                MOVE 9999 TO RETURN-CODE
                SET WS-FIN-LECTURA TO TRUE
             END-IF.

       F-9999-FINAL.
           EXIT.
      *
