       IDENTIFICATION DIVISION.
       PROGRAM-ID PGMCORT1.
      **********************************************************
      *                                                        *
      *                    ASINCRONICO 06                      *
      *                                                        *
      **********************************************************
      *  Realizar corte de control de campo Nro de Sucursal;   *
      *  ademês de sumar los importes por cada sucursal y el   *
      *  importe total de todo el archivo                      *
      *****************************************************************
      *   JCL    *                  ARCHIVOS                          *
      *****************************************************************
      * COMPILA  * ENTRADA: KC03C12.FIX.ASINCR06.PGMCORT1.CORTE       *
      * JCLCORT1 * SALIDA :                                           *
      *                                                               *
      *****************************************************************
      ****************************************
      *      MANTENIMIENTO DE PROGRAMA       *
      ****************************************
      *  FECHA     *    DETALLE        * COD *
      ****************************************
      * 10-04-2025 * CreaciÏn          * 001 *
      *            *                   *     *
      ****************************************
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
         SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.

      * ENTRADA es la variable interna del archivo CORTE y se asigna
      * al DDENTRA que es variable externa establecida en el JOB

      * El FS es una validador interno del estado del archivo asignado
       FILE-CONTROL                                                    .
             SELECT         ENTRADA
             ASSIGN         DDENTRA
             FILE STATUS IS WS-ENT-CODE                                .

      * Establecemos que ENTRADA es de 20 caracteres alfanumericos Fijo
       DATA DIVISION                                                   .
       FILE SECTION                                                    .
       FD ENTRADA
            BLOCK CONTAINS 0 RECORDS
            RECORDING MODE IS F                                        .

       01 REG-ENTRADA               PIC X(20)                          .

      **************************************
       WORKING-STORAGE SECTION.
      **************************************
       77  FILLER                   PIC X(26)      VALUE
            '* INICIO WORKING-STORAGE *'                               .

      *****  FILE STATUS  ******************
      * Variable establecida arriba para validar estado de archivo
       77  WS-ENT-CODE              PIC X(02).
           88  WS-ARCH-OK                          VALUE '00'          .
           88  WS-ARCH-FIN-LECTURA                 VALUE '10'          .
           88  WS-ARCH-ERROR                       VALUE '01' THRU '09'
                                                         '11' THRU '99'.

      *****  COPY DE ENTRADA  ***********************
           COPY CORTE.

      ********     CONSTANTES       ***************
       77  WS-SUCURSAL-NOM-ANT      PIC X(9)       VALUE "SUCURSAL "   .
       77  WS-TOTAL                 PIC X(24)      VALUE
            'TOTAL GENERAL IMPORTES: '                                 .
      ********  SUCURSALES A CONTROLAR  ******
       77  WS-SUCURSAL-NUM-ANT      PIC 9(2)       VALUE ZEROS         .

      ********  IMPORTE DE SUCURSALES  ******
       77  WS-SUCURSAL-IMP-ANT      PIC S9(7)V999  VALUE ZEROS         .
       77  WS-SUCUR-IMP-ANT-E       PIC ZZ.ZZZ.ZZ9,999
                                                   VALUE ZEROS         .

      ********  TOTAL  ******
       77  WS-CANT-IMP-TOTAL        PIC S9(8)V9(4) VALUE ZEROS         .
       77  WS-CANT-IMP-TOT-E        PIC ZZ.ZZZ.ZZ9,9999
                                                   VALUE ZEROS         .

       77  FILLER                   PIC X(26)      VALUE
            '* FINAL  WORKING-STORAGE *'                               .
      ***************************************************************

       PROCEDURE DIVISION.

      ***************
       MAIN-PROGRAM.
      ***************
           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    WS-ARCH-FIN-LECTURA
                OR    WS-ARCH-ERROR

           PERFORM    9999-FINAL
              THRU    9999-FINAL-F
           .

       MAIN-PROGRAM-F. GOBACK.

      ***************
       1000-INICIO.
      ***************
           SET        WS-ARCH-OK                   TO TRUE

           OPEN INPUT ENTRADA

           IF NOT     WS-ARCH-OK
              DISPLAY '* ERROR EN OPEN ENTRADA = '    WS-ENT-CODE
              MOVE    9999                         TO RETURN-CODE
           END-IF


      ****  PRIMERA LECTURA PARA CORTE CONTROL ***
           READ       ENTRADA                    INTO WS-REG-SUCURSAL

           EVALUATE   WS-ENT-CODE
             WHEN     '00'
      * Si todo OK, entonces mover NRO e IMPORTE de primer registro
      * le•do a variables internas
              MOVE    WS-SUC-NRO                  TO WS-SUCURSAL-NUM-ANT
              ADD     WS-SUC-IMPORTE              TO WS-SUCURSAL-IMP-ANT

             WHEN     '10'
              DISPLAY '* ARCHIVO ENTRADA VAC≤O      ' WS-ENT-CODE
              MOVE    9999                         TO RETURN-CODE

             WHEN OTHER
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE
              MOVE    9999                         TO RETURN-CODE
           END-EVALUATE
           .

       1000-INICIO-F.   EXIT.

      ***************
       2000-PROCESO.
      ***************
      * Leemos siguiente registro para evaluar si hubo cambios en el
      * valor de sucursal
           READ       ENTRADA                    INTO WS-REG-SUCURSAL

           EVALUATE   WS-ENT-CODE
      * Si lectura OK, comparamos el valor actual de SUCUR con anterior
             WHEN     '00'
              PERFORM 2200-SEGUIR-PROCESO
                 THRU 2200-SEGUIR-PROCESO-F

      * Si termina lectura de arch, entonces hace £ltimo corte de ctrl
             WHEN     '10'
              PERFORM 2230-CORTE-SUCURSAL
                 THRU 2230-CORTE-SUCURSAL-F

             WHEN OTHER
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE
              MOVE    9999                         TO RETURN-CODE

           END-EVALUATE
           .

       2000-PROCESO-F. EXIT.

      **********************
       2200-SEGUIR-PROCESO.
      **********************
      * Compara si hubo cambios o no en el valor de sucursal con el ante
           IF         WS-SUC-NRO               EQUAL WS-SUCURSAL-NUM-ANT

      * Si no hay cambios, aãado impor al impor-total de la sucur actual
              ADD     WS-SUC-IMPORTE              TO WS-SUCURSAL-IMP-ANT
           ELSE

      * Si la sucur actual es distinta a la anterior, ir a corte de ctrl
              PERFORM 2230-CORTE-SUCURSAL
                 THRU 2230-CORTE-SUCURSAL-F
           END-IF
           .

       2200-SEGUIR-PROCESO-F. EXIT.

      **********************
       2230-CORTE-SUCURSAL.
      **********************
           MOVE       WS-SUCURSAL-IMP-ANT         TO WS-SUCUR-IMP-ANT-E

      * Muestra total de importes acumulados por sucursal
           DISPLAY    WS-SUCURSAL-NOM-ANT
                      WS-SUCURSAL-NUM-ANT

           DISPLAY    "IMPORTE POR SUCURSAL: "
                      WS-SUCUR-IMP-ANT-E

           DISPLAY    "********************************************"

      * Guarda (copia) valor de nueva sucursal a la anterior
           MOVE       WS-SUC-NRO                  TO WS-SUCURSAL-NUM-ANT

      * Aãado importe acumulado de sucursal al importe total del arch
           ADD        WS-SUCURSAL-IMP-ANT         TO WS-CANT-IMP-TOTAL

      * Resetea el valor de los importes de sucursal a zero
           MOVE       ZEROS                       TO WS-SUCURSAL-IMP-ANT
                                                     WS-SUCUR-IMP-ANT-E

      * Muevo impor de sucursal actual a impor acumulado de nuev sucur
           ADD        WS-SUC-IMPORTE              TO WS-SUCURSAL-IMP-ANT
           .

       2230-CORTE-SUCURSAL-F. EXIT.

      ***************
       9999-FINAL.
      ***************
           MOVE       WS-CANT-IMP-TOTAL            TO WS-CANT-IMP-TOT-E
           DISPLAY    WS-TOTAL
                      WS-CANT-IMP-TOT-E
           DISPLAY    "----------------------------------"

      *
           CLOSE      ENTRADA
           IF         WS-ARCH-ERROR
              DISPLAY '* ERROR EN CLOSE ENTRADA = '
                      WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE
           END-IF
           .

       9999-FINAL-F.
           EXIT.
