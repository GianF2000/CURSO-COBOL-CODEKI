       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID PGMCORT1.                                             00011003
      **********************************************************        00012000
      *                                                        *        00013000
      *  PROGRAMA PARA EMITIR TOTAL CTAS ACTIVAS POR SUCURSAL  *        00014000
      *  ACTIVAS POR SUC Y  TOTAL CTAS ACTIVAS BCO             *        00015000
      *  LA ENTRADA CLASIFICADA X SUCURSAL                     *        00015100
      *                                                        *        00016000
      **********************************************************        00017000
      *      MANTENIMIENTO DE PROGRAMA                         *        00018000
      **********************************************************        00019000
      *  FECHA   *    DETALLE        * COD *                            00019100
      **************************************                            00019200
      *          *                   *     *                            00019300
      *          *                   *     *                            00019400
      **************************************                            00019500
       ENVIRONMENT DIVISION.                                            00020000
       CONFIGURATION SECTION.                                           00020106
         SPECIAL-NAMES.                                                 00020206
           DECIMAL-POINT IS COMMA.                                      00020306
       INPUT-OUTPUT SECTION.                                            00020400
       FILE-CONTROL.                                                    00020500
             SELECT ENTRADA ASSIGN DDENTRA                              00020600
                    FILE STATUS IS WS-ENT-CODE.                         00020700
                                                                        00020900
       DATA DIVISION.                                                   00039000
       FILE SECTION.                                                    00039100
       FD ENTRADA                                                       00039200
            BLOCK CONTAINS 0 RECORDS                                    00039300
            RECORDING MODE IS F.                                        00039400
                                                                        00039500
       01 REG-ENTRADA    PIC X(20).                                     00039604
                                                                        00039700
      **************************************                            00040400
       WORKING-STORAGE SECTION.                                         00040500
      **************************************                            00040600
       77  FILLER        PIC X(26) VALUE '* INICIO WORKING-STORAGE *'.  00040700
                                                                        00040808
      *****  FILE STATUS  ******************                            00040904
       77  WS-ENT-CODE      PIC XX    VALUE SPACES.                     00041000
       77  WS-SAL-CODE      PIC XX    VALUE SPACES.                     00041100
                                                                        00041200
       01  WS-STATUS-FIN    PIC X.                                      00041300
           88  WS-FIN-LECTURA         VALUE 'Y'.                        00041400
           88  WS-NO-FIN-LECTURA      VALUE 'N'.                        00041500
                                                                        00041600
      *****  COPY DE ENTRADA  ***********************                   00041905
           COPY CORTE.                                                  00042006
                                                                        00046200
      ********  TOTAL CTAS X SUCURSAL ******                            00046300
       77  WS-SUCURSAL-NOM-ANT   PIC X(9)        VALUE "SUCURSAL ".     00046405
       77  WS-SUCURSAL-NUM-ANT   PIC 9(2)        VALUE     ZEROS.       00046505
       77  WS-SUCURSAL-IMP-ANT   PIC S9(7)V999   VALUE ZEROS.           00046705
       77  WS-SUCUR-IMP-ANT-E    PIC ZZ.ZZZ.ZZ9,999 VALUE ZEROS.        00046806
      ********     CONSTANTES       ***************                     00046905
       77  WS-TOTAL PIC X(24) VALUE 'TOTAL GENERAL IMPORTES: '.         00047008
      ********  TOTAL   ******                                          00047205
       77  WS-CANT-IMP-TOTAL     PIC S9(8)V9(4)  VALUE ZEROS.           00047305
       77  WS-CANT-IMP-TOT-E     PIC ZZ.ZZZ.ZZ9,9999   VALUE ZEROS.     00047406
                                                                        00049000
       77  FILLER        PIC X(26) VALUE '* FINAL  WORKING-STORAGE *'.  00050800
      ***************************************************************.  00051000
       PROCEDURE DIVISION.                                              00051100
      **************************************                            00052000
      *  CUERPO PRINCIPAL DEL PROGRAMA     *                            00054000
      **************************************                            00057000
       MAIN-PROGRAM.                                                    00060000
           PERFORM 1000-INICIO  THRU   F-1000-INICIO.                   00060200
                                                                        00060300
           PERFORM 2000-PROCESO  THRU  F-2000-PROCESO                   00060400
                   UNTIL WS-FIN-LECTURA.                                00060700
                                                                        00061100
           PERFORM 9999-FINAL    THRU  F-9999-FINAL.                    00061200
                                                                        00061300
       F-MAIN-PROGRAM. GOBACK.                                          00061400
                                                                        00061500
      **************************************                            00061600
      *  CUERPO INICIO APERTURA ARCHIVOS   *                            00061800
      **************************************                            00062000
       1000-INICIO.                                                     00062100
           SET WS-NO-FIN-LECTURA TO TRUE.                               00070200
                                                                        00070300
           OPEN INPUT  ENTRADA.                                         00071000
           IF WS-ENT-CODE IS NOT EQUAL '00'                             00071100
              DISPLAY '* ERROR EN OPEN ENTRADA = ' WS-ENT-CODE          00071200
              MOVE 9999 TO RETURN-CODE                                  00071300
              SET  WS-FIN-LECTURA TO TRUE                               00071400
           END-IF.                                                      00071500
                                                                        00071600
      **************************************                            00073300
      *  PRIMER LECTURA PARA CORTE CONTROL *                            00073400
      **************************************                            00073500
                                                                        00073600
           READ ENTRADA INTO WS-REG-SUCURSAL                            00073706
                        AT END SET WS-FIN-LECTURA TO TRUE.              00073800
                                                                        00073900
           EVALUATE WS-ENT-CODE                                         00074000
              WHEN '00'                                                 00074105
      * Si todo OK, entonces mover NRO e IMPORTE a memoria de respaldo  00074208
                 MOVE WS-SUC-NRO          TO WS-SUCURSAL-NUM-ANT        00074305
                 ADD WS-SUC-IMPORTE       TO WS-SUCURSAL-IMP-ANT        00074405
                                                                        00074505
              WHEN '10'                                                 00074600
                  DISPLAY '* ARCHIVO ENTRADA VAC≤O      ' WS-ENT-CODE   00074705
                  MOVE 9999 TO RETURN-CODE                              00074805
                  SET WS-FIN-LECTURA TO TRUE                            00074905
                                                                        00075000
              WHEN OTHER                                                00075105
                  DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE   00075205
                  MOVE 9999 TO RETURN-CODE                              00075305
                  SET WS-FIN-LECTURA TO TRUE                            00075405
           END-EVALUATE.                                                00075500
                                                                        00075600
       F-1000-INICIO.   EXIT.                                           00075700
                                                                        00075800
      **************************************                            00075900
      *  CUERPO PRINCIPAL DE PROCESOS      *                            00076100
      *  LECTURA FILE INPUT                *                            00076208
      **************************************                            00076600
       2000-PROCESO.                                                    00076700
                                                                        00076800
           READ ENTRADA INTO WS-REG-SUCURSAL                            00077906
                        AT END SET WS-FIN-LECTURA TO TRUE.              00078000
                                                                        00078100
           EVALUATE WS-ENT-CODE                                         00078200
             WHEN '00'                                                  00078300
              PERFORM 2200-SEGUIR-PROCESO                               00078500
                 THRU F-2200-SEGUIR-PROCESO                             00078605
                                                                        00078700
             WHEN '10'                                                  00078805
              SET WS-FIN-LECTURA TO TRUE                                00078900
              PERFORM 2100-ULTIMO-REGISTRO                              00079000
                 THRU F-2100-ULTIMO-REGISTRO                            00079105
                                                                        00079200
             WHEN OTHER                                                 00079305
              DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE       00079400
              MOVE 9999 TO RETURN-CODE                                  00079500
              SET WS-FIN-LECTURA TO TRUE                                00079600
                                                                        00079700
           END-EVALUATE.                                                00079800
                                                                        00079900
                                                                        00080000
       F-2000-PROCESO. EXIT.                                            00080100
                                                                        00080200
      **************************************                            00080300
      *  WRITE ULTIMO REGISTRO DE SALIDA   *                            00080500
      **************************************                            00080700
       2100-ULTIMO-REGISTRO.                                            00080800
                                                                        00080900
           PERFORM 2230-CORTE-SUCURSAL THRU F-2230-CORTE-SUCURSAL.      00081008
                                                                        00081200
       F-2100-ULTIMO-REGISTRO. EXIT.                                    00083100
                                                                        00083200
      **************************************                            00083305
      *  SEGUIR PROCESO                    *                            00083405
      **************************************                            00083505
       2200-SEGUIR-PROCESO.                                             00083600
                                                                        00083700
           IF WS-SUC-NRO      EQUAL WS-SUCURSAL-NUM-ANT                 00083805
      * A∫ado importe al importe total de la sucursal en curso          00083908
              ADD WS-SUC-IMPORTE       TO WS-SUCURSAL-IMP-ANT           00084005
           ELSE                                                         00084100
                 PERFORM 2230-CORTE-SUCURSAL THRU                       00084200
                       F-2230-CORTE-SUCURSAL                            00084300
           END-IF.                                                      00084400
                                                                        00084500
       F-2200-SEGUIR-PROCESO. EXIT.                                     00084600
                                                                        00084700
      **************************************                            00084800
      *  PARRAFO QUE CONTROLA EL CORTE     *                            00084900
      *  DE SUCURSAL                       *                            00085000
      **************************************                            00085200
       2230-CORTE-SUCURSAL.                                             00085305
      ****************************************************              00086000
      *      MUESTRO TOTALES GRABADOS EN SALIDA          *              00086100
      ****************************************************              00086200
           MOVE WS-SUCURSAL-IMP-ANT TO WS-SUCUR-IMP-ANT-E               00086305
           DISPLAY WS-SUCURSAL-NOM-ANT       WS-SUCURSAL-NUM-ANT        00086405
           DISPLAY "IMPORTE POR SUCURSAL: "  WS-SUCUR-IMP-ANT-E         00086505
           DISPLAY "**********************************"                 00086607
      *                                                                 00086700
      ****************************************************              00087000
      * GUARDO LA SUCURSAL PARA PR‰XIMO CORTE SUCURSAL                  00166600
      ****************************************************              00166700
           MOVE WS-SUC-NRO          TO       WS-SUCURSAL-NUM-ANT        00166906
           ADD  WS-SUCURSAL-IMP-ANT TO       WS-CANT-IMP-TOTAL          00167105
           MOVE ZEROS               TO       WS-SUCURSAL-IMP-ANT        00167205
                                             WS-SUCUR-IMP-ANT-E         00167306
      * A∫ado importe al importe total de la sucursal en curso          00167408
           ADD WS-SUC-IMPORTE       TO       WS-SUCURSAL-IMP-ANT        00167506
           .                                                            00167606
       F-2230-CORTE-SUCURSAL. EXIT.                                     00168200
                                                                        00168300
      **************************************                            00171400
      *                                    *                            00171500
      *  CUERPO FINAL CIERRE DE FILES      *                            00171600
      *                                    *                            00171700
      **************************************                            00171800
       9999-FINAL.                                                      00171900
           MOVE WS-CANT-IMP-TOTAL TO WS-CANT-IMP-TOT-E.                 00172005
           DISPLAY WS-TOTAL WS-CANT-IMP-TOT-E.                          00172105
           DISPLAY "----------------------------------".                00172207
                                                                        00172305
           CLOSE ENTRADA                                                00172400
              IF WS-ENT-CODE IS NOT EQUAL '00'                          00172500
                DISPLAY '* ERROR EN CLOSE ENTRADA = '                   00172600
                                            WS-ENT-CODE                 00172700
                MOVE 9999 TO RETURN-CODE                                00172800
                SET WS-FIN-LECTURA TO TRUE                              00172900
             END-IF.                                                    00173000
                                                                        00173100
       F-9999-FINAL.                                                    00174000
           EXIT.                                                        00180000
      *                                                                 00200000
