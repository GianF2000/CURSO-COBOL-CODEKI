       IDENTIFICATION DIVISION.
       PROGRAM-ID. PGMCORT1.

      *---------------------------------------------------------------*
      * PROGRAMA   : PGMCORT1                                        *
      * AUTOR      : KC03C12                                         *
      * FECHA      : 10-04-2025                                      *
      * VERSION    : 001                                             *
      *---------------------------------------------------------------*
      * DESCRIPCIäN:                                                 *
      *   Realizar corte de control de campo Nro de Sucursal;        *
      *   sumar importes por cada sucursal y el importe total        *
      *   de todo el archivo.                                        *
      *---------------------------------------------------------------*
      * ENTRADAS   : Archivo CORTE (DDENTRA)                         *
      * SALIDAS    : Totales por sucursal y total general            *
      *---------------------------------------------------------------*
      * CäDIGOS DE RETORNO:                                          *
      *   00 = Ejecuciìn exitosa                                     *
      *   99 = Error inesperado                                      *
      *---------------------------------------------------------------*
      * MANTENIMIENTO                                                *
      * 10-04-2025 : Creaciìn inicial                                *
      *---------------------------------------------------------------*

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
         SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.

      *-------------------------------------------------------------*
      * FILE-CONTROL: Definiciìn de archivos y file status          *
      *-------------------------------------------------------------*
       FILE-CONTROL.
           SELECT    ENTRADA
           ASSIGN    DDENTRA
           FILE STATUS IS
                     WS-ENT-CODE
           .

       DATA DIVISION.
       FILE SECTION.
       FD  ENTRADA
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           .

       01  REG-ENTRADA                 PIC X(20)
           .

      *-------------------------------------------------------------*
      * WORKING-STORAGE SECTION: Variables, copys y control         *
      *-------------------------------------------------------------*
       WORKING-STORAGE SECTION.
       77  FILLER                      PIC X(26)   VALUE
            '* INICIO WORKING-STORAGE *'
           .

      * COPY de estructura de entrada
           COPY CORTE
           .

      * FILE STATUS para ENTRADA
       77  WS-ENT-CODE                 PIC X(02)
           .
           88  WS-ARCH-OK                          VALUE '00'
           .
           88  WS-ARCH-FIN-LECTURA                 VALUE '10'
           .
           88  WS-ARCH-ERROR                       VALUE '01' THRU '09'
                                                         '11' THRU '99'
           .

      * Constantes
       77  WS-SUCURSAL-NOM-ANT         PIC X(9)    VALUE 'SUCURSAL '
           .
       77  WS-TOTAL                    PIC X(24)   VALUE
            'TOTAL GENERAL IMPORTES: '
           .

      * Sucursales a controlar
       77  WS-SUCURSAL-NUM-ANT         PIC 9(2)    VALUE ZEROS
           .

      * Importe de sucursales
       77  WS-SUCURSAL-IMP-ANT         PIC S9(7)V999
                                                   VALUE ZEROS
           .
       77  WS-SUCUR-IMP-ANT-E          PIC ZZ.ZZZ.ZZ9,999
                                                   VALUE ZEROS
           .

      * Total
       77  WS-CANT-IMP-TOTAL           PIC S9(8)V9(4)
                                                   VALUE ZEROS
           .
       77  WS-CANT-IMP-TOT-E           PIC ZZ.ZZZ.ZZ9,9999
                                                   VALUE ZEROS
           .

      *
       77  FILLER                      PIC X(26)
                                                   VALUE
            '* FINAL  WORKING-STORAGE *'
           .

       PROCEDURE DIVISION.

      ****************
       MAIN-PROGRAM.
      ****************
      * Control principal del flujo del programa
           PERFORM    1000-INICIO
              THRU    1000-INICIO-F

           PERFORM    2000-PROCESO
              THRU    2000-PROCESO-F
             UNTIL    WS-ARCH-FIN-LECTURA
                OR    WS-ARCH-ERROR

           PERFORM    9999-FINAL
              THRU    9999-FINAL-F
           .

       MAIN-PROGRAM-F. GOBACK.

      ****************
       1000-INICIO.
      ****************
      * Inicializa variables y abre archivo de entrada
           SET        WS-ARCH-OK                   TO TRUE

           OPEN INPUT ENTRADA

           IF         WS-ARCH-ERROR
              DISPLAY '* ERROR EN OPEN ENTRADA = '
                      WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE

              PERFORM 99999-RUTINA-ERROR
                 THRU 99999-ERROR-F
           END-IF

      * Primera lectura para validar que archivo no est© vac¥o
           READ       ENTRADA                    INTO WS-REG-SUCURSAL

           EVALUATE TRUE
              WHEN WS-ARCH-OK
      * Mueve valores de sucursal a variables de control y acumulaciìn
                 MOVE WS-SUC-NRO                  TO WS-SUCURSAL-NUM-ANT

                 ADD  WS-SUC-IMPORTE              TO WS-SUCURSAL-IMP-ANT

              WHEN WS-ARCH-FIN-LECTURA
                 DISPLAY '* ARCHIVO ENTRADA VAC²O      '
                         WS-ENT-CODE

                 MOVE    9999                         TO RETURN-CODE

              WHEN OTHER
                 DISPLAY '* ERROR EN LECTURA ENTRADA = ' WS-ENT-CODE

                 MOVE    9999                         TO RETURN-CODE

                 PERFORM 99999-RUTINA-ERROR
                    THRU 99999-ERROR-F
           END-EVALUATE
           .

       1000-INICIO-F.   EXIT.

      ****************
       2000-PROCESO.
      ***************
      * Leemos siguiente registro para evaluar si hubo cambios en el
      * valor de sucursal
           READ       ENTRADA                    INTO WS-REG-SUCURSAL

           EVALUATE   TRUE
      * Si lectura OK, comparamos el valor actual de SUCUR con anterior
             WHEN     WS-ARCH-OK
              PERFORM 2200-SEGUIR-PROCESO
                 THRU 2200-SEGUIR-PROCESO-F

      * Si termina lectura de arch, entonces hace £ltimo corte de ctrl
             WHEN     WS-ARCH-FIN-LECTURA
              PERFORM 2230-CORTE-SUCURSAL
                 THRU 2230-CORTE-SUCURSAL-F

             WHEN OTHER
              DISPLAY '* ERROR EN LECTURA ENTRADA = '
                      WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE

              PERFORM 99999-RUTINA-ERROR
                    THRU 99999-ERROR-F

           END-EVALUATE
           .

       2000-PROCESO-F. EXIT.

      **********************
       2200-SEGUIR-PROCESO.
      **********************
      * Compara si hubo cambios o no en el valor de sucursal con el ante
           IF         WS-SUC-NRO               EQUAL WS-SUCURSAL-NUM-ANT

      * Si no hay cambios, a‹ado impor al impor-total de la sucur actual
              ADD     WS-SUC-IMPORTE              TO WS-SUCURSAL-IMP-ANT
           ELSE

      * Si la sucur actual es distinta a la anterior, ir a corte de ctrl
              PERFORM 2230-CORTE-SUCURSAL
                 THRU 2230-CORTE-SUCURSAL-F
           END-IF
           .

       2200-SEGUIR-PROCESO-F. EXIT.

      **********************
       2230-CORTE-SUCURSAL.
      **********************
           MOVE       WS-SUCURSAL-IMP-ANT         TO WS-SUCUR-IMP-ANT-E

      * Muestra total de importes acumulados por sucursal
           DISPLAY    WS-SUCURSAL-NOM-ANT
                      WS-SUCURSAL-NUM-ANT

           DISPLAY    "IMPORTE POR SUCURSAL: "
                      WS-SUCUR-IMP-ANT-E

           DISPLAY    "********************************************"

      * Guarda (copia) valor de nueva sucursal a la anterior
           MOVE       WS-SUC-NRO                  TO WS-SUCURSAL-NUM-ANT

      * A‹ado importe acumulado de sucursal al importe total del arch
           ADD        WS-SUCURSAL-IMP-ANT         TO WS-CANT-IMP-TOTAL

      * Resetea el valor de los importes de sucursal a zero
           MOVE       ZEROS                       TO WS-SUCURSAL-IMP-ANT
                                                     WS-SUCUR-IMP-ANT-E

      * Muevo impor de sucursal actual a impor acumulado de nuev sucur
           ADD        WS-SUC-IMPORTE              TO WS-SUCURSAL-IMP-ANT
           .

       2230-CORTE-SUCURSAL-F. EXIT.

      ***************
       9999-FINAL.
      ***************
           MOVE       WS-CANT-IMP-TOTAL            TO WS-CANT-IMP-TOT-E

           DISPLAY    WS-TOTAL
                      WS-CANT-IMP-TOT-E

           DISPLAY    "----------------------------------"

      *
           CLOSE      ENTRADA
           IF         WS-ARCH-ERROR
              DISPLAY '* ERROR EN CLOSE ENTRADA = '
                      WS-ENT-CODE

              MOVE    9999                         TO RETURN-CODE
           END-IF
           .

       9999-FINAL-F. EXIT.

      **********************
       99999-RUTINA-ERROR.
      **********************
           GOBACK.

       99999-ERROR-F. EXIT.
